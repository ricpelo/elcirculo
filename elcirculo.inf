!% -~S
!% -G
!% +language_name=Spanish
!% $MAX_STATIC_DATA=20000
!% $MAX_PROP_TABLE_SIZE=63840
!% $MAX_VERBS=200
!% $MAX_ZCODE_SIZE=45000
!% $MAX_VERBSPACE=4500

!
! EL CÍRCULO
!
! Copyright (c) 2012 Ricardo Pérez López (Alpha Aventuras)
!

! This program is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! This program is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.


!===============================================================================
! (1) Constantes; Variables Globales; Replaces; Propertys; Incluir Parser.h
!-------------------------------------------------------------------------------


! ====================================================================
! CONSTANTES
! ====================================================================


Constant Story "~EL CÍRCULO~";
Constant Headline "^(c) 2012 Alpha Aventuras^^
                  Teclea CRÉDITOS si quieres saber más acerca de la aventura, o
                  AYUDA cuando estés bloqueado.^";
Release 1;

! Queremos usar el comando 'Salidas'
Constant ADMITIR_COMANDO_SALIDAS;
! No queremos usar los comandos 'Lugares' y 'Objetos'
Constant NO_PLACES;
! No queremos usar puntuación
Constant NO_SCORE;
! Queremos que se muestren las deducciones del parser
!Constant IMPRIMIR_DEDUCCIONES;
! Hay curiosidades al final del juego
Constant AMUSING_PROVIDED;

! Alturas de los gráficos:
Constant GRAFICOS_MINUSCULOS = 200;
Constant GRAFICOS_PEQUENOS   = 250;
Constant GRAFICOS_MEDIANOS   = 350;
Constant GRAFICOS_GRANDES    = 450;

Constant CANAL_FONDO = 9;      Constant VOLUMEN_FONDO = 50;
Constant CANAL_INCIDENTAL = 0; Constant VOLUMEN_INCIDENTAL = 50;
Constant CANAL_TV_SALON = 1;   Constant VOLUMEN_TV_SALON = 100;
Constant CANAL_TV_CUARTO = 2;  Constant VOLUMEN_TV_CUARTO = 50;
Constant CANAL_TELEFONO = 3;   Constant VOLUMEN_TELEFONO = 100;

! Vamos a usar Damusix junto con SGW+DMX
Constant SGW_CON_DAMUSIX;
Constant SONIDO_BUCLE_INFINITO = -1;

! El color de fondo de la ventana gráfica es negro:
Constant CLR_GG_PERSBACK = CLR_GG_BLACK;

Constant TIMER_TICK = 1000;
Constant TIMER_DURACION_TLF_KATIE = 6;
Constant TIMER_DURACION_BECCA = 5;


! ====================================================================
! VARIABLES GLOBALES
! ====================================================================


! Alto de la ventana gráfica:
Global altoVentanaGrafica = GRAFICOS_MEDIANOS;

! Hay gráficos o no:
Global hayGraficos = true;

! Hay sonidos:
Global haySonido = true;
Global haySonidoFondo = true;

! Hay eventos en tiempo real:
Global hayTiempoReal = true;

! Hay efecto de teletipo:
Global hayTeletipo = false;

! Si se ha activado el listado automático de salidas:
Global mostrarSalidas = true;

! Se pone a true y false en la rutina PermitirEmpujarDir, y luego se consulta en
! MensajesLibreria:
Global estoyEmpujando = false;

! Interruptor de pistas:
Global hayPistas = 0;

! Interruptor para indicar si hay que dibujar o no la línea de estado
! (ver rutina Inicialise):
Global dibujarEstado = false;

! La imagen que representa la oscuridad:
Global SGW_IMAGEN_OSCURIDAD = Oscuridad_jpg;


! ====================================================================
! REPLACES
! ====================================================================


! Nuestra versión de esta rutina sólo redibuja la línea de estado cuando
! la variable global dibujarEstado está a true:
Replace DrawStatusLine;

! En este juego no se usa el BuscarEn (MIRA EN, BUSCA EN/DENTRO/DE/SOBRE,
! REGISTRA...):
Replace SearchSub;

! En este juego no se usa el Consultar (CONSULTA, LEE, BUSCA EN x SOBRE y...):
Replace ConsultSub;

! En este juego no se habla, ni se responde, ni se dice:
Replace TellSub;
Replace AnswerSub;
Replace DecirSub;

! Con el siguiente Replace conseguimos que el objeto empujado hacia una
! dirección aparezca en la descripción de la localidad de destino del "empuje":
Replace AllowPushDir;

! Ponemos nuestra propia versión de ZIPI_RunMenu y ZIPI_RunOtro,
! para el menú principal y las pistas:
Replace ZIPI_RunMenu;
Replace ZIPI_RunOtro;

! Nuestra versión elimina también los signos de admiración e interrogación:
Replace QuitarAcentos;

Replace InitGlkWindow;
Replace HandleGlkEvent;

! Usamos el ParseNoun de IntNombre_NG:
Replace ParseNoun;

! Lo pide Timer.h:
Replace KeyDelay;

Include "Parser";


!===============================================================================
! (2) Puntos de Entrada para Glk [solo en Glulx]:
!     - InitGlkWindow(), IdentifyGlkObject(), HandleGlkEvent()
!-------------------------------------------------------------------------------


[ IdentifyGlkObject fase tipo ref rock;
  SGW_IdentifyGlk(fase,tipo,ref,rock);
  Damusix.IdentificarSonidos(fase);
!  Mapa_IdentifyGlkObject(fase, tipo, ref, rock);
];


[ InitGlkWindow winrock;
  switch (winrock) {
    GG_MAINWIN_ROCK:
      glk_stylehint_set(wintype_TextBuffer, style_Normal,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextBuffer, style_Normal,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_Normal,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Normal,
                        stylehint_Weight, 0);
      glk_stylehint_set(wintype_TextBuffer, style_Normal,
                        stylehint_Proportional, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Normal,
                        stylehint_Justification, stylehint_just_LeftRight);

      glk_stylehint_set(wintype_TextBuffer, style_Input,
                        stylehint_TextColor, $a1d0fc);
      glk_stylehint_set(wintype_TextBuffer, style_Input,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_Input,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Input,
                        stylehint_Proportional, 0);
      glk_stylehint_set(wintype_TextBuffer, style_Input,
                        stylehint_Weight, 1);

      ! Esta será mi negrita:
      glk_stylehint_set(wintype_TextBuffer, style_User1,
                        stylehint_TextColor, $a1d0fc);
      glk_stylehint_set(wintype_TextBuffer, style_User1,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_User1,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_User1,
                        stylehint_Proportional, 1);
      glk_stylehint_set(wintype_TextBuffer, style_User1,
                        stylehint_Weight, 1);

      glk_stylehint_set(wintype_TextBuffer, style_User2,
                        stylehint_TextColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_User2,
                        stylehint_BackColor, $00ff00);
      glk_stylehint_set(wintype_TextBuffer, style_User2,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_User2,
                        stylehint_Proportional, 0);
      glk_stylehint_set(wintype_TextBuffer, style_User2,
                        stylehint_Weight, 1);

      glk_stylehint_set(wintype_TextBuffer, style_Alert,
                        stylehint_TextColor, $00ff00);
      glk_stylehint_set(wintype_TextBuffer, style_Alert,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_Alert,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Alert,
                        stylehint_Proportional, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Alert,
                        stylehint_Oblique, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Alert,
                        stylehint_Weight, 1);
 
      glk_stylehint_set(wintype_TextBuffer, style_Emphasized,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextBuffer, style_Emphasized,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_Emphasized,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Emphasized,
                        stylehint_Proportional, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Emphasized,
                        stylehint_Oblique, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Emphasized,
                        stylehint_Weight, 1);

      glk_stylehint_set(wintype_TextBuffer, style_Preformatted,
                        stylehint_TextColor, $82c1fb);
      glk_stylehint_set(wintype_TextBuffer, style_Preformatted,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_Preformatted,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Preformatted,
                        stylehint_Proportional, 0);
      glk_stylehint_set(wintype_TextBuffer, style_Preformatted,
                        stylehint_Weight, 0);

      glk_stylehint_set(wintype_TextBuffer, style_Header,
                        stylehint_TextColor, $a1d0fc);
      glk_stylehint_set(wintype_TextBuffer, style_Header,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_Header,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Header,
                        stylehint_Weight, 1);

      glk_stylehint_set(wintype_TextBuffer, style_Subheader,
                        stylehint_TextColor, $a1d0fc);
      glk_stylehint_set(wintype_TextBuffer, style_Subheader,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_SubHeader,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_SubHeader,
                        stylehint_Weight, 1);

      glk_stylehint_set(wintype_TextBuffer, style_Note,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextBuffer, style_Note,
                        stylehint_BackColor, $000000);

      glk_stylehint_set(wintype_TextBuffer, style_BlockQuote,
                        stylehint_TextColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_BlockQuote,
                        stylehint_BackColor, $ffffff);


      glk_stylehint_set(wintype_TextGrid, style_Normal,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextGrid, style_Normal,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_Normal,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_Normal,
                        stylehint_Weight, 0);
      glk_stylehint_set(wintype_TextGrid, style_Normal,
                        stylehint_Proportional, 0);
      glk_stylehint_set(wintype_TextGrid, style_Normal,
                        stylehint_Justification, stylehint_just_LeftRight);
         
      glk_stylehint_set(wintype_TextGrid, style_Input,
                        stylehint_TextColor, $a1d0fc);
      glk_stylehint_set(wintype_TextGrid, style_Input,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_Input,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_Input,
                        stylehint_Proportional, 0);
      glk_stylehint_set(wintype_TextGrid, style_Input,
                        stylehint_Weight, 1);
 
      glk_stylehint_set(wintype_TextGrid, style_User1,
                        stylehint_TextColor, $a1d0fc);
      glk_stylehint_set(wintype_TextGrid, style_User1,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_User1,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_User1,
                        stylehint_Proportional, 0);
      glk_stylehint_set(wintype_TextGrid, style_User1,
                        stylehint_Weight, 1);
  
      glk_stylehint_set(wintype_TextGrid, style_User2,
                        stylehint_TextColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_User2,
                        stylehint_BackColor, $00ff00);
      glk_stylehint_set(wintype_TextGrid, style_User2,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_User2,
                        stylehint_Proportional, 0);
      glk_stylehint_set(wintype_TextGrid, style_User2,
                        stylehint_Weight, 1);

      glk_stylehint_set(wintype_TextGrid, style_Alert,
                        stylehint_TextColor, $00ff00);
      glk_stylehint_set(wintype_TextGrid, style_Alert,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_Alert,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_Alert,
                        stylehint_Proportional, 0);
      glk_stylehint_set(wintype_TextGrid, style_Alert,
                        stylehint_Oblique, 1);
      glk_stylehint_set(wintype_TextGrid, style_Alert,
                        stylehint_Weight, 1);

      glk_stylehint_set(wintype_TextGrid, style_Emphasized,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextGrid, style_Emphasized,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_Emphasized,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_Emphasized,
                        stylehint_Proportional, 0);
      glk_stylehint_set(wintype_TextGrid, style_Emphasized,
                        stylehint_Oblique, 1);
      glk_stylehint_set(wintype_TextGrid, style_Emphasized,
                        stylehint_Weight, 1);
      glk_stylehint_set(wintype_TextGrid, style_Emphasized,
                        stylehint_Justification, stylehint_just_LeftRight);

      glk_stylehint_set(wintype_TextGrid, style_Preformatted,
                        stylehint_TextColor, $82c1fb);
      glk_stylehint_set(wintype_TextGrid, style_Preformatted,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_Preformatted,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_Preformatted,
                        stylehint_Proportional, 0);
      glk_stylehint_set(wintype_TextGrid, style_Preformatted,
                        stylehint_Weight, 1);

      glk_stylehint_set(wintype_TextGrid, style_Header,
                        stylehint_TextColor, $a1d0fc);
      glk_stylehint_set(wintype_TextGrid, style_Header,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_Header,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_Header,
                        stylehint_Weight, 1);

      glk_stylehint_set(wintype_TextGrid, style_Subheader,
                        stylehint_TextColor, $a1d0fc);
      glk_stylehint_set(wintype_TextGrid, style_Subheader,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_SubHeader,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_SubHeader,
                        stylehint_Weight, 1);

      glk_stylehint_set(wintype_TextGrid, style_Note,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextGrid, style_Note,
                        stylehint_BackColor, $000000);

      glk_stylehint_set(wintype_TextGrid, style_BlockQuote,
                        stylehint_TextColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_BlockQuote,
                        stylehint_BackColor, $ffffff);
  }

  rfalse; ! si te olvidas esta linea, el juego no funcionará bien
];


[ HandleGlkEvent ev context buffer;
  SGW_HandleGlk(ev);
  ControlTimer.CT_HandleGlkEvent(ev, context, buffer);
!  Mapa_HandleGlkEvent(ev, context, buffer);
];


!===============================================================================
! (3) Modificar Mensajes de la Libreria; Otros Includes; Incluir VerbLib.h
!-------------------------------------------------------------------------------


!Include "Rastros_NG";


Object LibraryMessages
  with
    before [;
!      Look:
!        ! Copiamos el código de Mensajes.h. Sólo cambiamos el segundo
!        ! EscribirListaDesde, para que cuando se deje en el suelo la
!        ! linterna, no diga: "Puedes ver una linterna (en la que ves una batería)."
!        switch (lm_n) {
!          1: print " (sobre ", (the) lm_o, ")";
!          2: print " (en ", (the) lm_o, ")";
!          3: print " (como ", (object) lm_o, ")";
!          4: print "^Sobre ", (the) lm_o;
!             WriteListFrom(child(lm_o), ENGLISH_BIT + RECURSE_BIT +
!                           PARTINV_BIT + TERSE_BIT + ISARE_BIT + CONCEAL_BIT);
!             ".";
!          default:
!            if (lm_o ~= location) {
!              if (lm_o has supporter)
!                print "^Sobre ";
!              else
!                print "^En ";
!              print (the) lm_o;
!              print " puedes ver ";
!            } else
!              print "^También puedes ver ";
!            if (n == 5) print "también ";    ! ?????
!            ! Quitamos RECURSIVO_BIT e INFOPARCIAL_BIT:
!            WriteListFrom(child(lm_o), ENGLISH_BIT + WORKFLAG_BIT +
!                          TERSE_BIT + CONCEAL_BIT);
!            if (lm_o ~= location) ".";
!            ".";
!        }

      Prompt:
        ImprimirPrompt();
        rtrue;

      Go:
        switch (lm_n) {
          2: if (estoyEmpujando)
               "^No ", (puedes_) " seguir empujando porque no hay salida
               hacia ", (the) noun, ".";
             "No ", (puedes_) " ir por ahí, porque no hay salida hacia ",
              (the) noun, ".";
          5: if (estoyEmpujando)
               "^No ", (puedes_) " empujar nada a través ", (del) lm_o, ".";
             "No ", (puedes_) " pasar a través ", (del) lm_o, ".";
        }
        
      Take:
        if (lm_n == 1) {
          print "Reco";
          if (player.persona == PRIMERA_PERSONA + PERSONA_SINGULAR) print "j";
          else                                                      print "g";
          print (es_) " ";
          if (lm_o has moved) print (the) lm_o;
          else                print (a) lm_o;
          ".";
        }

      Drop:
        if (lm_n == 4) "Dej", (as_) " ", (the) lm_o, ".";

      Remove:
        if (lm_n == 3) {
          if (verb_word == 'quita') {
            "Quit", (as_) " ", (the) lm_o, " ", (del) second, ".";
          } else {
            "Sac", (as_) " ",  (the) lm_o, " ", (del) second, ".";
          }
        }
      Miscellany:
        switch (lm_n) {
           4: TextoLlamativo(" ¡Enhorabuena! Has acabado la aventura ");
              rtrue;
          10: rtrue;
          13: ! Reactivamos timer en caso de UNDO:
              ControlTimer.ReactivarTick();
              "[Retrocediendo al turno anterior.]";
          17: switch (random(4)) {
                1: "Está muy oscuro y no puedes ver nada.";
                2: "Todo está demasiado oscuro y no aprecias nada con
                    claridad.";
                3: "La oscuridad te impide ver dónde te encuentras.";
                4: "Te mueves a tientas a través de una oscuridad que te impide
                    ver nada.";
              }
          19: "Has tenido días mejores...";
          30: switch (random(3)) {
                1: "No encuentro eso que dices.";
                2: "No veo nada parecido por aquí.";
                3: "Parece que no hay nada de eso.";
              };
          27: "Las palabras se te agolpan en los labios.";
          38: switch (random(5)) {
                1: "¿Qué quieres decir con eso?";
                2: "Lo siento, no te entiendo.";
                3: "Intenta ser un poco más preciso.";
                4: "No comprendo eso que dices.";
                5: "¿Qué intentas decir?";
              }
        }
    ];


! Hay que definirla antes de incluir Moviles_NG:
Class Lugar
!  class LugarConRastro
  with
!   sgw_mus Estancia_ogg,
!   sgw_vol VOLUMEN_FONDO,
    describe [;
      PrintOrRun(self, description);
      if (mostrarSalidas) {
        print "^";
        glk_set_style(style_Emphasized);
        <Salidas>;
        glk_set_style(style_Normal);
        rtrue;
      }
    ],
    number,
    dibujado false,                   ! Necesario para Mapeador.h
    before [;
      Examine, Touch:
        if (noun == d_obj) {          ! Examinamos el suelo
          "No observas nada en especial al mirar al suelo.";
        } else if (noun == u_obj) {   ! Examinamos el techo
          "No observas nada en especial al mirar al techo.";
        }        
    ],
  has
    light;

    
Include "SGW+DMX_NG";       ! Entre EParser y Acciones
Include "elcirculo.bli";    ! Los recursos multimedia de la aventura
Include "VerbLib";
Include "Barra";            ! Barra de estado personalizable
Include "Moviles_NG";       ! La librería Moviles retocada por mí
Include "PNJpuertas_NG";    ! La librería PnjPuertas retocada por mí (muy poco)
Include "PNJactor_NG";      ! Librerías que hacen que un PNJ pueda coger y dejar
Include "PNJacciones_NG";   ! Poner detrás de Moviles y PnjPuertas
Include "Etemas";           ! Los temas de conversación
Include "Decorado_NG";      ! Objetos de decorado
Include "ExaminarFalso";    ! Poder examinar objetos sólo con su nombre
Include "Teletipo";         ! Teletipo para sacar mensajes letra a letra
Include "Pistas";           ! Las pistas creadas con ZIPI
Include "Timer";            ! Mi librería de eventos temporizados
Include "IntNombre_NG";     ! Poder usar adjetivos como en InformATE
Include "EscenarioAbrible"; ! Escenario que se puede abrir y cerrar
Include "Contenedor";       ! Contenedores
Include "EventList_NG";     ! Listas de mensajes


!===============================================================================
! (4) Rutinas Initialise() e IniciarAventura()
!-------------------------------------------------------------------------------


[ Initialise r v;
  IniciarGraficosSonidos();
  openGraphicWindow(250);
  clearGraphicWindow();
  StatusLineHeight(23);
  clearMainWindow();
  viewImageCenter(Logo_Alpha_Aventuras_Corto_jpg);
  v = Damusix.QueVolumenCanal(CANAL_FONDO);
  Damusix.AsignarCanal(Darkwalk_ogg, CANAL_FONDO, v, SONIDO_BUCLE_INFINITO);
  Damusix.Tocar(Darkwalk_ogg);
  ZIPI_Intro();
.Menu;
  r = ZIPI_RunMenu(ZIPI_Menu_Principal, true);
  if (r == 3) jump Menu;
  if (r ~= 2) quit;
  StatusLineHeight(1);
  if (hayGraficos) EncenderGraficos();
  else             ApagarGraficos();
  clearTextWindow();
];


[ IniciarAventura logo;

  location = thedark;

  dibujarEstado = false;

  ! Esto es para que el juego dé siempre la descripción de la habitación, aunque
  ! ya la hayamos visitado
  lookmode = 2;

  ! Inventario en una sola frase
  inventory_style = ENGLISH_BIT + RECURSE_BIT + FULLINV_BIT;

  ! Parseado estricto (de la librería IntNombre):
  parseado_estricto = 1;

  ! Nombre de la oscuridad
  thedark.short_name = "Zona de oscuridad";
  
  ! Localización donde comienza el jugador
  location = cuarto_katie;

!  AsignarPersona(PRIMERA_PERSONA + PERSONA_SINGULAR);
!  give player female;

!  clearTextWindow();

  IniciarMoviles();
  IniciarPuertas();
!  IniciarRastros();

  if (logo) {
    clearTextWindow();
    clearGraphicWindow();
!    DibujaLogoAlien();
  }

  Damusix.PararCanal(CANAL_FONDO);
  sonido_telefono_katie.sonar();
  timer_telefono_katie.AsignarGestor(1);
  timer_becca.AsignarGestor(2);
  ControlTimer.ActivarTick(TIMER_TICK);
  ! Esto hay que ponerlo para que no aparezca tu otro yo moviéndose solo:
  PNJ_Ruta(katie, MOVIMIENTO_NINGUNO);
  ChangePlayer(katie, 1);
  PNJ_Ruta(becca, MOVIMIENTO_PERSEGUIR, katie);
  KeyDelay();
];


!===============================================================================
! (5) Rutinas de la Libreria Implementadas, Reemplazadas o Ampliadas
!-------------------------------------------------------------------------------


[ InScope;
  ! Así le podemos pedir a Becca que coja el teléfono aunque el teléfono no
  ! esté presente:
  if (actor == becca && LugarReal() ofclass LugarCasaKatie) {
    PlaceInScope(telefono_katie);
  }
  rfalse;
];


[ Amusing;
  rfalse;
];


[ DrawStatusLine;
  if (dibujarEstado) {
    barra_estado.dibujar();
  }
];


! Modificado para que ?, !, ¿, ¡ se separen de las palabras al parsear:
[ QuitarAcentos buffer parse x i word at len;
  for (x = 0: x < parse-->0: x++) { ! para cada palabra
    word = tokenDict(parse, x);
    at   = tokenPos(parse, x);
    len  = WordLength(x);
    if (word == 0) { ! no comprendida
      for (i = at: i < at + len: i++)
        ! [080625] Quitados caracteres en mayúscula
        switch(buffer->i) {
          225: buffer->i = 'a';       ! 225: á
          233: buffer->i = 'e';       ! 233: é
          237: buffer->i = 'i';       ! 237: í
          243: buffer->i = 'o';       ! 243: ó
     250, 252: buffer->i = 'u';       ! 250: ú, 252: ü
          241: buffer->i = 'n';       ! 241: ñ
          '?': buffer->i = ' ';
          191: buffer->i = ' ';
          '!': buffer->i = ' ';
          161: buffer->i = ' ' ;
        }
        Tokenise__(buffer, parse);
      }
  }
];


! Modificado para que al empujar un objeto a una localidad se muestre
! más información:
[ AllowPushDir i tmp;
  if (parent(second) ~= compass) return L__M(##PushDir, 2, noun);
  if (second == u_obj or d_obj)  return L__M(##PushDir, 3, noun);
  AfterRoutines(); i = noun; move i to player;
  tmp = location;                                     ! (c) Alpha
  estoyEmpujando = true;                              ! (c) Alpha
  <Go second>;
  estoyEmpujando = false;                             ! (c) Alpha
  if (location == thedark) move i to real_location;
  else                     move i to location;
  if (tmp ~= location && location ~= thedark)
    PrintOrRun(i, describe);                          ! (c) Alpha
];


!===============================================================================
! (6) Incluir SpanishG.h; Otros Includes
!-------------------------------------------------------------------------------


Include "SpanishG";
Include "Decir";           ! Para poder usar DECIR ... A ...
!Include "Mapeador";


!===============================================================================
! (7) Clases del Juego
!-------------------------------------------------------------------------------


!Class MiTeletipo
!  class Teletipo,
!  with
!    gancho_antes [;
!!     Damusix.TocarCanal(CANAL_TELETIPO);
!    ],
!    gancho_despues [;
!!     Damusix.PararCanal(CANAL_TELETIPO);
!    ];


Class LugarCasaKatie
  class Lugar
  with
    after [ o;
      Go:
        objectloop (o ofclass Sonido && o in self) {
          o.cambiar_volumen();
        }
    ];


Class Sonido
  with
    cambiar_volumen [;
      Damusix.VolumenCanal(self.canal, self.volumen());
    ],
    sonando [;
      return Damusix.SonandoDeFondoCanal(self.canal);
    ],
    tocar_sonido [ snd;
      Damusix.AsignarCanal(snd, self.canal, self.volumen(),
                           SONIDO_BUCLE_INFINITO);
      Damusix.TocarCanal(self.canal);
    ],
    parar_sonido [;
      Damusix.PararCanal(self.canal);
    ];


!Class MensajesSecuencia
!  with
!    contador -1,
!    total [;
!      return self.#mensajes / WORDSIZE;
!    ],
!    acabado [;
!      return self.contador == self.total() - 1;
!    ],
!    siguiente [
!      t r;
!      t = self.total();
!      if (t == 0) {
!        "*** ERROR: SECUENCIA DE MENSAJES VACÍA ***";
!      } else if (self.contador < t - 1) {
!        self.contador++;
!        r = self.&mensajes-->(self.contador);
!        return r;
!      } else {
!        return self.&mensajes-->(t - 1);
!      }
!    ];


!===============================================================================
! (8) Objetos del Juego
!-------------------------------------------------------------------------------


! ====================================================================
! HABITACIONES Y DECORADOS
! ====================================================================

! ---------------------------------------------
! PRÓLOGO
! ---------------------------------------------


Object cuarto_katie "Tu habitación"
  class LugarCasaKatie
  with
    description "La pequeña lámpara de la mesita situada junto a tu cama
                 proyecta débiles y verdosos haces de luz que iluminan tu
                 habitación con un tono que, en estos momentos, te resulta
                 enfermizo y asfixiante. El tamaño de tu cama parece pugnar
                 por abarcar el mayor espacio posible de la estancia, sólo
                 equilibrado por el pequeño televisor que observa todo sin
                 parpadear su único ojo desde una pequeña mesa situada en
                 la pared contraria.",
    out_to pasillo_katie,
    sgw_img Oscuridad_jpg,
    compass_look [ d;
      if (d == out_obj) {
          "A través de la puerta entreabierta observas el pasillo en penumbra.";
      }
    ];


Object decorado_cuarto_katie cuarto_katie
  class Decorado
  with
    before [;
      Open:
        if (self.palabra == 'puerta') {
          "Ya estaba abierta.";
        }

      Close:
        if (self.palabra == 'puerta') {
          HablaBecca("¿Pero no vas a salir a coger el teléfono?");
          rtrue;
        }

      Take, Push, PushDir:
        if (self.palabra == 'cama') {
          "Es demasiado pesada.";
        }
    ],
    sinonimos
      'mesa' 'mesita' (-1),
    describe
      'cama' "Te gusta dormir cómoda, y por ello tu cama tiene casi el doble
              de ancho que una cama normal, pero ahora, sin tener muy claro
              el por qué, se te antoja poco acogedora." G_FEMENINO
!      'silla' "Una silla." G_FEMENINO
      'lampara' "Una lámpara que emite una tenue luz especialmente verdosa." 
                G_FEMENINO
      'mesita' "Una baja mesita al fondo de la habitación sostiene,
                solitaria, el televisor del dormitorio." G_FEMENINO
      'puerta' "La puerta de tu habitación permanece entreabierta.";


Object pasillo_katie "Pasillo del piso superior"
  class LugarCasaKatie
  with
    description [;
      print "Nunca, hasta ahora, el pasillo del piso superior de tu casa
             te había parecido tan oscuro y carente de alma. Una fría
             corriente de aire lo recorre de extremo a extremo, subiendo
             por las escaleras que dan a la planta baja y cruzando tu
             espalda en escalofríos continuos y persistentes.";
      if (puerta_katie notin self) {
        " La puerta abierta a tu habitación es prácticamente la única
         fuente de luz en este lúgubre pasaje."; 
      } else {
        new_line;
      }
    ],
    in_to cuarto_katie,
    d_to cocina_katie,
    sgw_img Oscuridad_jpg;


Object decorado_pasillo_katie pasillo_katie
  class Decorado
  with
    before [;
      Open:
        "Ya estaba abierta.";

      Close:
        "Pensándolo bien, será mejor no cerrarla. Perderías la mayor parte de
         la poca luz que ilumina el pasillo.";
    ],
    describe
      'puerta' "La puerta entreabierta muestra una vista parcial de tu
                habitación, cercándola como el marco de un cuadro vertical y
                alargado."
                G_FEMENINO;

    
Object cocina_katie "Cocina"
  class LugarCasaKatie
  with
    description "La cocina, sobria y funcional, es el núcleo central del
                 piso inferior, el distribuidor por el que llegar a los
                 restantes lugares de la casa. El frigorífico y el teléfono
                 son los dos únicos elementos destacables en una estancia,
                 por otro lado, gélida y casi en penumbra. A través de un
                 enorme arco sin puerta se accede al salón de la casa.",
    u_to pasillo_katie,
    s_to salon_katie,
    sgw_img Oscuridad_jpg,
    before [;
      Go:
        if (noun == u_obj) {
          if ((~~(sonido_telefono_katie.sonando())) &&
              tv_salon_katie.enchufado) {
            switch (tv_salon_katie.encendidos_solo) {
              0: if (tv_salon_katie has on) {
                   tv_salon_katie.apagar();
                   print "Justo cuando ibas a salir de la cocina para subir al
                          piso de arriba, oyes que el televisor se apaga solo
                          en el salón. Te giras para comprobar que,
                          efectivamente, el salón está a oscuras y el silencio
                          parece ahora más intenso que nunca.";
                   KeyDelay();
                   print " Instantes después, vuelve a encenderse, dejando en
                          el ambiente un sonido de estática tan carente de
                          calidez y alma como el mismo silencio.^";
                 } else {
                   print "Justo cuando ibas a salir de la cocina para subir al
                          piso de arriba, oyes que el televisor se enciende
                          solo en el salón. Te giras lentamente para comprobar
                          que, en efecto, el salón brilla y parpadea al ritmo
                          de un sonido de estática tam carente de calidez y
                          alma como el mismo silencio.^";
                 }
                 tv_salon_katie.encender();
                 HablaKatie("Becca, deja de bromear... ¿Dónde estas?");
                 print "^Miras a tu alrededor, pero sigues estando sola.^";
                 rtrue;

              1: if (tv_salon_katie has on) {
                   jump NoDejarEncendido;
                 } else {
                   tv_salon_katie.se_enciende_por_segunda_vez(false);
                   rtrue;
                 }

              default:
                if (tv_salon_katie has on) {
              .NoDejarEncendido;
                  "Algo te dice que no quieres dejar el televisor encendido.
                   Algo inexplicable, irracional y primario.";
                }
            }
          }
        }
    ];


Object decorado_cocina_katie cocina_katie
  class Decorado
  with
    describe
      'arco' "La pared sur se abre en forma de arco para acceder al salón." 0;


Object salon_katie "Salón"
  class LugarCasaKatie
  with
    description "El salón de tu casa está claramente dominado por un televisor
                 de gran tamaño situado sobre una mesa en la pared frontal.
                 Junto al enorme sofá a unos tres metros de la misma forman
                 en la práctica el único mobiliario disponible, si exceptuamos
                 un par de sillas a ambos lados del sofá y algunos cuadros
                 colgados de las paredes. Una ventana abierta deja pasar la
                 oscuridad del exterior.",
    n_to cocina_katie,
    before [;
      Go:
        if (noun == n_obj && frigorifico_katie has open && 
            frigorifico_katie hasnt general) {
          give frigorifico_katie general;
          print "Nada más cruzar el arco que separa la cocina del salón, notas
                 algo moverse junto al teléfono, justo en el extremo de tu
                 percepción visual. Al girar la mirada, ves la causa y el
                 corazón te da un vuelco: la puerta del frigorífico se abre
                 poco a poco hasta quedar a medio abrir.";
          KeyDelay();
          new_line;
        }
    ],
    sgw_img Oscuridad_jpg;


Object decorado_salon_katie salon_katie
  class Decorado
  with
    sinonimos
      'silla' 'sillas' G_FEMENINO,
    describe
      'sillas' "Son dos sillas, una a cada lado del sofá central."
               G_FEMENINO + G_PLURAL
      'sofa'   "Aunque sabes que resulta cómodo una vez que te sientas, al tacto
                resulta resbaladizo, frío y duro. Permanece estático en el
                centro de la estancia." 0
      'mesa'   "La mesa que sostiene el televisor tiene el tamaño justo para
                albergar un aparato de tales dimensiones." G_FEMENINO;
    
    
! ====================================================================
! OBJETOS QUE LLEVAN LOS PERSONAJES AL EMPEZAR
! ====================================================================


! ====================================================================
! OTROS OBJETOS
! ====================================================================


Object telefono_katie "teléfono" cocina_katie
  with
    name 'telefono' 'auricular' 'tlf',
    description "El teléfono de tu casa, de un color blanco gastado, cuelga de
                 la pared junto al frigorífico.",
!    initial "Ves tu teléfono.",
    sonando [;
      return sonido_telefono_katie.sonando();
    ],
    descolgar [;
      sonido_telefono_katie.parar(true);
      remove becca;
      becca_ausente.found_in = true;
      move becca_ausente to LugarReal();
      move charco_katie to pasillo_katie;
      move puerta_katie to pasillo_katie;
    ],
    becca_lo_coge [;
      self.descolgar();
      print "Becca coge el teléfono con expresión seria.^";
      HablaBecca("¿Diga?", true);
      print " -la oyes decir, casi susurrando,
            al auricular del teléfono.^^";
      print "Lentamente, a medida que escucha lo que el teléfono le dice, se
             gira hacia ti con una inclasificable mirada en los ojos, y tu
             preocupación crece por momentos...";
      KeyDelay(2000);
      print " hasta que te muestra una amplia sonrisa y pasándote el teléfono,
             divertida, te dice:^";
      HablaBecca("Es tu madre");
      new_line;
      self.coges_el_telefono();
    ],
    coges_el_telefono [;
      print "Coges el teléfono, y aún con desconfianza lo acercas a tu oído.
             Tras emitir un nervioso ~¿Hola?~, oyes al otro lado de la línea
             la tranquilizadora (y enfática) voz de tu madre preguntándote qué
             tal va la noche y exhortando que no te vayas tarde a la cama.
             Durante la corta pero extrañamente tranquilizadora conversación,
             percibes la voz de Becca preguntando algo mientras se aleja de la
             cocina escaleras arriba. Finalmente, cuelgas el teléfono...";
      KeyDelay(5000);
      print (s_emph) " y estas sola.^";
      HablaKatie("¿Becca?", true);
      print " -la llamas temiendo alzar demasiado la voz, sin respuesta.^";
      HablaKatie("¿BECCA?", true);
      print " -gritas esta vez, sacando fuerzas del miedo.^^";
      print_ret (s_emph) "No hay respuesta.";
!      print (s_user1) "¿Becca?");
    ],
    before [;
      Take, Descolgar:
        if (self.sonando()) {
          self.descolgar();
          self.coges_el_telefono();
          rtrue;
        } else {
          "Descuelgas y no oyes nada. Ni siquiera el tono de marcado.";
        }

      Drop, Colgar:
        "El teléfono ya está colgado.";

      LlamarPor:
        if (self.sonando()) {
          "El teléfono está sonando. No puedes llamar tú ahora.";
        } else {
          <<Descolgar self>>;
        }
    ],
  has
    scenery;


Object frigorifico_katie "frigorífico" cocina_katie
  class EscenarioAbrible
  with
    name_m 'frigorifico' 'congelador',
    name_f 'nevera' 'puerta',
    article "el",
    description [;
      if (self hasnt open) {
        "Un moderno frigorífico de puertas metálicas y casi imperceptible
         ronroneo.";
      } else {
        "El frigorífico, entreabierto, emite un haz de luz azulado bañando
         parte de la cocina a su paso. El sonido del motor resulta más
         audible que cuando estaba cerrado.";
      }
    ],
    ya_visto_abierto false,
    when_open [;
      if (~~(self.ya_visto_abierto)) {
        self.ya_visto_abierto = true;
        "^Ves el frigorífico abierto, como una invitación a lo desconocido."; 
      } else {
        "^El frigorífico sigue abierto.";
      }
    ],
    before [;
      Open:
        if (self hasnt general) {
          "Abres el frigorífico, observas la comida con nulo interés, y vuelves
           a cerrarlo cuestionándote brevemente el por qué de tu falta de
           apetito.";
        } else {
          "No te atreves a volverlo a abrir.";
        }

      Close:
        if (self hasnt open) "El frigorífico ya está cerrado.";
    ],
    after [;
      Close:
        self.cerrar();
        "Cierras el frigorífico con rapidez sin atreverte a mirar en su
        interior.";
    ];


Object tv_cuarto_katie "televisor" cuarto_katie
  with
    name_f 'tv' 'television' 'tele' 'pantalla',
    name_m 'televisor',
    description [;
      print "Un pequeño televisor sin aspectos destacables. Está ";
      if (self has on) print "encendido, emitiendo nieve y ruido de estática";
      else             print "apagado";
      ".";
    ],
!    initial "Ves el televisor.",
    encender [;
      give self general;
      sonido_tv_cuarto_katie.sonar();
    ],
    apagar [;
      sonido_tv_cuarto_katie.parar();
    ],
    before [;
      Examine:
        PrintOrRun(self, description);
        rtrue;

      Enchufar:
        "El televisor ya estaba enchufado.";
        
      Desenchufar:
        "¿Para qué quieres desenchufarlo?";
    ],
    after [ c;
      SwitchOn:
        c = self hasnt general;
        self.encender();
        if (c) {
          "Al encender el televisor, te sorprende comprobar que no aparece
           ninguna imagen, sino simple ruido y estática, como si no recibiera
           señal.";
        } else {
          "Vuelves a encender el televisor y el ruido granulado aparece de
           nuevo en la pantalla acompañado de estática.";
        }

      SwitchOff:
        self.apagar();
        "Apagas el televisor, sin evitar sentir cierto alivio al hacerlo.";
    ],
  has
    scenery switchable;


Object tv_salon_katie "televisor" salon_katie
  with
    name_f 'tv' 'television' 'tele' 'pantalla',
    name_m 'televisor',
    description [;
      if (self hasnt general) {
        "Un televisor negro, impersonal, de gran tamaño, con una pantalla
         tan oscura que su superficie apenas refleja el entorno en el que
         se encuentra.";
      } else {
        if (self has on) {
          "El enorme televisor ilumina la estancia con un brillo vivo y
           cambiante, resultado del perpetuo movimiento del ruido en la
           pantalla, que te recuerda al de millones de organismos
           microscópicos moviéndose y chocando entre sí.";
        } else {
          "El televisor, ahora apagado, se presenta tan oscuro como un mal
           presagio.";
        }
      }
    ],
    describe [;
      if (self has on) {
        "^El televisor está encendido, mostrando un ruido granulado que baña
         el salón con una luz sucia y nerviosa.";
      }
      rtrue;
    ],
!    initial "Ves el televisor.",
    time_left,
    time_out [;
      if (self hasnt on) {
        self.se_enciende_por_segunda_vez(true);
!      "^Todos los músculos del cuerpo se te agarrotan y tus pulmones se quedan
!       sin aire cuando notas que el televisor del salón vuelve a encenderse
!       solo."
!      Se vuelve a encender el televisor.";
      }
    ],
    se_enciende_por_segunda_vez [ sw;
      ! sw == false si viene de cocina_katie al intentar subir
      ! sw == true  si viene del time_out de tv_salon_katie
      self.encender();
      Damusix.AsignarCanal(Gathering_Darkness_ogg, CANAL_INCIDENTAL,
                           VOLUMEN_INCIDENTAL, SONIDO_BUCLE_INFINITO);
      Damusix.TocarCanal(CANAL_INCIDENTAL);
      if (~~sw) {
        print "El televisor se vuelve a encender. Solo. Otra vez.
               Más sonido de estática proveniente del salón,
               transformando la atmósfera en pura inquietud. Ahora
               sí que puedes sentir con claridad los latidos de tu
               corazón rebotando en tu pecho de pura ansiedad.^";
      } else {
        if (LugarReal() ~= salon_katie) {
          print "^Un momento... ¿Eso que oyes... no es el televisor?^";
        }
        print "^Durante unos instantes no acabas de creerlo... Buscas una
               explicación lógica, intentas convencerte a ti misma de que no
               es cierto... Pero la realidad, injusta pero imparcial, te lo
               deja claro: el televisor del salón ha vuelto a encenderse
               solo.^";
      }
      HablaKatie("¡Becca, vale ya!");
      "^Pero Becca no está. Y te quedas ahí, sin subir la escalera, mirando
       en dirección al salón y el televisor encendido.";
    ],
    enchufado true,
    encendidos_solo 0,
    encender [ sw;
      give self general;
      give self on;
      sonido_tv_salon_katie.sonar();
      if (~~sw) self.encendidos_solo++;
    ],
    apagar [;
      give self ~on;
      sonido_tv_salon_katie.parar();
    ],
    before [;
      Examine:
        PrintOrRun(self, description);
        rtrue;

      SwitchOn:
!        if (~~(self.enchufado))
!          "No se puede encender porque está desenchufado.";
        if (self has on) "Ya está encendido.";
        rfalse;
        
      SwitchOff:
!        if (~~(self.enchufado))
!          "Ya está desenchufado.";
        if (self hasnt on) "Ya está apagado.";
        rfalse;

      Enchufar:
        "El televisor ya está enchufado.";
        
      Desenchufar:
        "El cable del enchufe pasa por la parte trasera y no alcanzas a tirar
         de él.";
        
!      Tie:
!        if (self.enchufado) {
!          "El televisor ya está enchufado.";
!        } else {
!          self.enchufado = true;
!          "Enchufas el televisor.";
!        }

!      Desenchufar:
!        if (~~(self.enchufado)) {
!          "El televisor ya está desenchufado.";
!        } else {
!          self.enchufado = false;
!          if (self has on) {
!            self.apagar();
!            "Desenchufas el televisor. Al hacerlo, la pantalla se apaga.";
!          } else {
!            "Desenchufas el televisor.";
!          }
!        }     
    ],
    after [;
      SwitchOn:
        self.encender(true);
        StopTimer(self);
        if (self hasnt general) {
          give self general;
          "Al encender el televisor compruebas que no aparece ninguna emisión
           salvo la típica imagen de ruido granulado y el sonido de estática
           que se oye siempre que no se capta ninguna señal.";
        } else {
          "Vuelves a encender el televisor y éste vuelve a mostrar ruido y a
           emitir sonido de estática.";
        }

      SwitchOff:
        self.apagar();
        switch (self.encendidos_solo) {
          1: StartTimer(self, 3);
          2: frigorifico_katie.abrir();
        }
        "Con un gesto rápido, apagas el televisor y la estática desaparece,
         retornando la estancia a la semioscuridad.";
    ],
  has
    static switchable;


Object charco_katie "charco de agua"
  with
    name 'charco' 'agua',
    description "Es, claramente, un charco con forma irregular
                 alimentado del agua que se filtra por debajo de la puerta
                 de tu habitación.",
    initial [;
      StartTimer(self, 4);
      "Depositado en mitad del pasillo, como una mancha líquida en el
       suelo, ves un charco de agua que se extiende filtrándose por
       debajo de la puerta cerrada de tu habitación.";
    ],
    time_left 0,
    time_out [;
      give pomo_puerta_katie ~scenery;
      give pomo_puerta_katie static;
      self.initial = "El charco es cada vez más grande. No deja de salir agua
                      por debajo de la puerta.";
    ],
    react_before [;
      Examine:
        if (noun == d_obj) {
          <<Examine self>>;
        }
    ],
    before [;
      Smell:
        "El agua expele un hedor putrefacto, podrido, como el del agua
        estancada.";
    ],
  has
    static;


Object puerta_katie "puerta de tu habitación"
  with
    name 'puerta' 'habitacion',
    article "la",
    description "Bajo la puerta cerrada de tu habitación se escurre hacia el
                 pasillo un flujo lento pero constante de agua. Un resplandor
                 blanquecino proveniente de tu habitación acompaña al río de
                 agua mientras atraviesa la rendija inferior de la puerta. Del
                 pomo, empapado como si rezumara humedad, caen continuas gotas
                 al suelo.",
    initial "Alguien ha cerrado la puerta de tu habitación, dejando bajo ella
             una fina abertura por la que salen un líquído hediondo y una
             luz mortecina.",
    add_to_scope pomo_puerta_katie,
    react_before [;
      Enter, GoIn:
        <<Open self>>;

      Examine:
        rfalse;
        
      default:
        if (noun == pomo_puerta_katie) {
          noun = puerta_katie;
          ! Esto es porque al hacer el truco de la línea anterior (de cambiar
          ! el noun por la cara) luego no se ejecuta la rutina before:
          return RunRoutines(self, before);
        }
        rfalse;
    ],
    before [;
      Examine:
        PrintOrRun(self, description);
        rtrue;

      Open:
        FinPrologo();
        rtrue;
        
      Close:
        "La puerta ya está cerrada.";
        
    ],
  has
    static female openable;


Object pomo_puerta_katie "pomo de la puerta"
  with
    name 'pomo' 'picaporte',
    description "Del pomo de la puerta cae un goteo continuo de agua que no
                 acabas de comprender de dónde proviene.",
    add_to_scope gotas_pomo_puerta,
    before [;
      Touch:
        "Está mojado.";

      Take:
        <<Open puerta_katie>>;
    ],
  has
    static;


Object gotas_pomo_puerta "gotas"
  with
    name_f 'gota',
    name_fp 'gotas',
    before [;
      Examine:
        <<Examine pomo_puerta_katie>>;

      Touch:
        <<Touch pomo_puerta_katie>>;
        
      default:
        "Las gotas de agua son casi inmateriales y se escurren entre tus
         dedos.";
    ],
  has
    scenery concealed;


! ====================================================================
! PERSONAJES
! ====================================================================


Object katie "Katie" cuarto_katie
  class Movil
  with
    name 'katie',
    description "Eres tú: Katie Embry, una chica adolescente algo alocada pero
                 responsable, de ojos oscuros y cabello largo, rubio y liso.
                 Tu mejor amiga Becca y tú estáis pasando una noche solas en tu
                 casa, aprovechando que tus padres están fuera. Aún llevas
                 puesto tu uniforme escolar, aunque vas descalza.",
  has
    female;


Object becca "Becca" cuarto_katie
  class Movil
  with
    name 'becca',
    description "Becca es, sin duda, tu mejor amiga, y lo es desde os
                 conocísteis, hace ya siglos. Es morena, con grandes ojos
                 verdes, y de carácter un tanto rebelde. Compañeras de
                 Instituto que sois, lleva puesto el mismo uniforme que tú.",
    initial "^Junto a ti está Becca.",
    describe [;
      switch (random(3)) {
        1: "^A tu lado, tu amiga Becca te observa con inquietud.";
        2: "^Becca te mira esperando qué vas a hacer.";
        3: "^Tu amiga Becca está a tu lado.";
      }
    ],
    react_before [;
      Gritar:
        HablaBecca("¡¿Pero qué te pasa?! ¿Qué ocurre? ¿A qué vienen esos
                    gritos?");
        rtrue;
    ],
    react_after [ c;
      SwitchOn:
        if (noun == tv_cuarto_katie or tv_salon_katie) {
          c = tv_cuarto_katie hasnt general && tv_salon_katie hasnt general;
          ! Lo hago así para que el mensaje de Becca aparezca DESPUÉS de los
          ! mensajes que genere la rutina after:
          RunRoutines(noun, after);
          if (c) {
            HablaBecca("Qué extraño... ¿Qué ha pasado con la imagen?");
          } else {
            HablaBecca("Sigue sin haber imagen...");
          }
          rtrue;
        }
    ],
    before [;
      Llamar:
        HablaBecca("Estoy aquí... ¿No me ves?");
        rtrue;
    ],
    life [
      tema;
      Ask:
        tema = AveriguarTema(TemasBecca);
        switch (tema) {
          tema_becca_telefono:
            HablaBecca("¿Crees que puede ser la misma persona que te llamó
                        en la cabaña?");
            PreguntaSiNo = 1;

          tema_becca_tv:
            if (tv_cuarto_katie has general || tv_salon_katie has general) {
              HablaBecca("Parece que se ha ido la señal.");
            } else {
              HablaBecca("Sí, haces bien en apagar la tele. Es un rollo.");
            }
            
          default:
            HablaBecca("No entiendo qué quieres decirme.");
        }
        rtrue;
    ],
    orders [;
      if (player == self) rfalse;
      Go, GoIn, Enter, Exit, Subir, Bajar:
        HablaBecca("No pienso ir sola a ninguna parte.");
        rtrue;
        
      Take:
        if (noun == telefono_katie) {
          if (LugarReal() == cocina_katie) {
            telefono_katie.becca_lo_coge();
            rtrue;
          } else {
            HablaBecca("¿Dónde está el teléfono?");
            rtrue;
          }
        }

      NotUnderstood, Tell:
        <<Ask self>>;

      default:
        HablaBecca("Mejor hazlo tú misma.");
        rtrue;
    ],
  has
    animate proper;


Object becca_ausente "Becca ausente"
  with
    name 'becca',
    description [;
      if (location == cocina_katie or salon_katie) {
        "No ves a Becca por ninguna parte. ¿Estará arriba?";
      } else {
        "No ves a Becca por ninguna parte. ¿Estará en tu habitación?";
      }
    ],
    found_in false,
    before [;
      Llamar:
        "Llamas a Becca, pero no obtienes respuesta.";

      Gritar:
        "Gritas el nombre de Becca, sin obtener respuesta.";

      default:
        "Becca no está.";
    ],
    life [;
      Ask:
        "Nadie responde.";
    ],
    orders [;
      NotUnderstood, Tell:
        <<Ask self>>;

      default:
        "No ves a Becca por ninguna parte.";
    ],
  has
    animate concealed;

    
! ====================================================================
! GESTORES DE TIMER
! ====================================================================


Object timer_telefono_katie
  class GestorTimer
  with
    duracion TIMER_DURACION_TLF_KATIE,
    condicion [;
      return sonido_telefono_katie.sonando();
    ],
    evento [;
      if (sonido_telefono_katie.time_left <= 0) {
        sonido_telefono_katie.time_left = sonido_telefono_katie.duracion_alarma;
        sonido_telefono_katie.time_out();
      } else {
        sonido_telefono_katie.time_left--;
      }
    ];

    
Object timer_becca
  class GestorTimer
  with
    duracion TIMER_DURACION_BECCA,
    condicion [;
      return sonido_telefono_katie.sonando();
    ],
    evento [;
      sonido_telefono_katie.each_turn();
    ];


! ====================================================================
! OBJETOS DE SOPORTE
! ====================================================================


Object barra_estado
  class objeto_barra_estado
  with
    modo BE_COMPUESTO,
    disposicion
      1  1 true BE_LOCALIDAD
      68 1 true BE_TURNOS,
    lineas_inv
      BE_INV_TOTAL;


Object sonido_telefono_katie
  class Sonido
  with
    found_in cuarto_katie pasillo_katie cocina_katie salon_katie,
    canal CANAL_TELEFONO,
    duracion_alarma 10,
    time_left,
    time_out [;
      if (self.sonando()) {
        StartTimer(self, self.duracion_alarma);
        self.avisar_que_suena();
      }
    ],
    avisar_que_suena [;
      ControlTimer.PrepararImpresion();
      "^El teléfono sigue sonando.";
    ],
    volumen [
      l v;
      l = LugarReal(); 
      if (l == cocina_katie)       v = VOLUMEN_TELEFONO;
      else if (l == salon_katie)   v = VOLUMEN_TELEFONO / 2;
      else if (l == pasillo_katie) v = VOLUMEN_TELEFONO / 2;
      else if (l == cuarto_katie)  v = VOLUMEN_TELEFONO / 4;
      else                         v = 0;
      return v;
    ],
    sonar [ sw;
      self.tocar_sonido(Telefono1_ogg);
      StartTimer(self, self.duracion_alarma);
      if (~~sw) "Son las diez de la noche en punto y, súbitamente, comienza a
                 sonar el teléfono en la planta baja.";
    ],
    parar [ sw;
      self.parar_sonido();
      StopTimer(self);
      if (~~sw) "El teléfono deja de sonar.";
    ],
    each_turn [;
      if (self.sonando()) {
        if (becca in cocina_katie && random(100) < 10 &&
            msg_becca.at_end()) {
          ControlTimer.PrepararImpresion();
          HablaBecca("Venga ya, esto es ridículo...");
          new_line;
          telefono_katie.becca_lo_coge();
        } else if (random(100) < 20) {
          ControlTimer.PrepararImpresion();
          HablaBecca(msg_becca.run_script());
        }
      }
    ],
  has
    concealed;


Object sonido_tv_salon_katie
  class Sonido
  with
    found_in salon_katie cocina_katie pasillo_katie,
    canal CANAL_TV_SALON,
    volumen [
      l v;
      l = LugarReal(); 
      if (l == salon_katie)        v = VOLUMEN_TV_SALON;
      else if (l == cocina_katie)  v = VOLUMEN_TV_SALON / 2;
      else if (l == pasillo_katie) v = VOLUMEN_TV_SALON / 4;
      else                         v = 0;
      return v;
    ],
    sonar [;
      self.tocar_sonido(TV_Ruido_ogg);
    ],
    parar [;
      self.parar_sonido();
    ],
  has
    concealed; 


Object sonido_tv_cuarto_katie
  class Sonido
  with
    found_in salon_katie pasillo_katie,
    canal CANAL_TV_CUARTO,
    volumen [
      l v;
      l = LugarReal(); 
      if (l == cuarto_katie)       v = VOLUMEN_TV_CUARTO;
      else if (l == pasillo_katie) v = VOLUMEN_TV_CUARTO / 3;
      else                         v = 0;
      return v;
    ],
    sonar [;
      self.tocar_sonido(TV_Ruido_ogg);
    ],
    parar [;
      self.parar_sonido();
    ],
  has
    concealed; 


Object msg_becca
  class StopEventList
  with
    event_list "Katie... Está sonando el teléfono..."
               "¿Qué pasa? ¿No lo coges?"
               "¿Por qué no lo coges?"
               "¡Pero cógelo ya! ¿Tienes miedo o qué?";
  

!Object msg_becca
!  class MensajesSecuencia
!  with
!    mensajes "Katie... Está sonando el teléfono..."
!             "¿Qué pasa? ¿No lo coges?"
!             "¿Por qué no lo coges?"
!             "¡Pero cógelo ya! ¿Tienes miedo o qué?";


! ====================================================================
! TEMAS RAIZ
! ====================================================================


! Cuidado con los infinitivos. Si pones un infinitivo aquí, luego no se puede
! usar como verbo

Object TemasBecca;

Object tema_becca_telefono "el teléfono" TemasBecca
  with
    name 'telefono' 'tlf' 'auricular';

Object tema_becca_tv "el televisor" TemasBecca
  with
    name 'televisor' 'tv' 'television' 'pantalla';


! ====================================================================
! MENÚS ZIPI
! ====================================================================


[ ZIPI_RunOtro _o
  r;
  ZIPI_PintaTitulo(_o.ZIPI_titulo);
  print "^";
  r = _o.ZIPI_ejecutar();
  if (~~r) ZIPI_Espera();
  return r;
];


[ ZIPI_RunMenu _m top
  i j count cur key target redibujar ft r;

  redibujar = 1;
  cur = 0;
  count = _m.#ZIPI_item / WORDSIZE; count = count + 2;
  ft = true;

  for (::) {
    if (redibujar) {
      ZIPI_PintaTitulo(_m.ZIPI_titulo);
      redibujar = 0;
    }

    for (i = 0: i < count - 2: i++) {
      j = i + 3;
      ZIPI_setcursor(5,j);
!      if (ft && hayTeletipo) {
!        tt_computadora.visualiza((_m.&ZIPI_item-->i).ZIPI_titulo);
!      } else {
        style fixed;
        if ((_m.&ZIPI_item-->i).ZIPI_titulo ofclass Routine)
          (_m.&ZIPI_item-->i).ZIPI_titulo();
        else print (string) (_m.&ZIPI_item-->i).ZIPI_titulo;
        style roman;
!      }
    }

    j = i + 4;
    ZIPI_setcursor(5, j);

!    if (ft) {
!      if (top) tt_computadora.visualiza("Salir");
!      else     tt_computadora.visualiza("Volver");
!    } else {
      if (top) print (s_pref) "Salir";
      else     print (s_pref) "Volver";
!    }

    ft = false;
    j = cur + 3;
    ZIPI_setcursor(2, j);
    print ">";
    ZIPI_setcursor(2, j);
    key = ZIPI_tecla();
    print " ";

    if (key > ZIPI_EVENTO_HYPER) {
      cur = key - ZIPI_EVENTO_HYPER - 2;
      if (cur == -1) key = 27;
      else           key = 13;
    }

    switch(key) {
      'k', 'p', '-', '_', 129, -4:
        do {
          cur--;
          if (cur == count - 2) cur--;
          if (cur < 0) cur = count - 1;
        } until ((_m.&ZIPI_item-->cur) ~= ZIPI_Separador);
        break;
      'j', 'n', '=', '+', 130, -5:
        do {
          cur++;
          if (cur == count - 2) cur++;
          if (cur >= count) cur = 0;
        } until ((_m.&ZIPI_item-->cur) ~= ZIPI_Separador);
        break;
      'q', 'Q', 27, 131, 10, 8, -2:
        rfalse;
 
      132, 13, 'n', ' ', -3, -6:
        if (cur == count - 1) rfalse;
        if (cur == count - 2) break;
        target = _m.&ZIPI_item-->cur;
        if (target ofclass ZIPI_Menu)          ZIPI_RunMenu(target);
        else if (target ofclass ZIPI_Pista)    ZIPI_RunPista(target);
        else if (target provides ZIPI_cambiar) target.ZIPI_cambiar();
        else {
          r = ZIPI_RunOtro(target);
          if (target == ZIPI_Restaurar) return 3;
          if (r == 2) return r;
        }
        redibujar = 1;
        break;
    }
  }
  rfalse;
];


Object ZIPI_Menu_Principal
  class ZIPI_Menu
  with
    ZIPI_titulo "EL CÍRCULO - (c) Alpha Aventuras 2012",
    ZIPI_item
      ZIPI_Jugar_Con_Intro
      ZIPI_Jugar_Sin_Intro
      ZIPI_Restaurar
      ZIPI_Configuracion
      ZIPI_Separador
      ZIPI_Menu1;

      
Object ZIPI_Configuracion
  class ZIPI_Menu
  with
    ZIPI_titulo "Opciones de gráficos y sonido",
    ZIPI_item
      ZIPI_Graficos
      ZIPI_Tamano_Graficos
      ZIPI_Sonido
      ZIPI_Sonido_Fondo
      ZIPI_Separador
      ZIPI_Accesibilidad;

      
Object ZIPI_Graficos
  class ZIPI_Otro
  with
    ZIPI_titulo [;
      if (hayGraficos) "Gráficos: SÍ (recomendado)";
      else             "Gráficos: NO";
    ],
    ZIPI_cambiar [;
      hayGraficos = ~~hayGraficos;
    ],
    ZIPI_ejecutar [; rtrue; ];

    
Object ZIPI_Tamano_Graficos
  class ZIPI_Otro
  with
    ZIPI_titulo [;
      switch (altoVentanaGrafica) {
        GRAFICOS_MINUSCULOS: "Tamaño de gráficos: ",
                             GRAFICOS_MINUSCULOS, " px. (MINÚSCULOS)";
        GRAFICOS_PEQUENOS:   "Tamaño de gráficos: ",
                             GRAFICOS_PEQUENOS, " px. (PEQUEÑOS)";
        GRAFICOS_MEDIANOS:   "Tamaño de gráficos: ",
                             GRAFICOS_MEDIANOS, " px. (MEDIANOS, recomendado)";
        GRAFICOS_GRANDES:    "Tamaño de gráficos: ",
                             GRAFICOS_GRANDES,  " px. (GRANDES)";
        default:             "Tamaño de gráficos: ",
                             altoVentanaGrafica, " px.";          
      }
    ],
    ZIPI_cambiar [;
      if (altoVentanaGrafica < GRAFICOS_GRANDES)
        altoVentanaGrafica = altoVentanaGrafica + 50;
      else
        altoVentanaGrafica = GRAFICOS_MINUSCULOS;
    ],
    ZIPI_ejecutar [; rtrue; ];

    
Object ZIPI_Sonido
  class ZIPI_Otro
  with
    ZIPI_titulo [;
      if (haySonido) "Sonido global: SÍ (recomendado)";
      else           "Sonido global: NO";
    ],
    ZIPI_cambiar [;
      haySonido = ~~haySonido;
      if (haySonido) Damusix.ActivarAudio();
      else           Damusix.DesactivarAudio();
    ],
    ZIPI_ejecutar [; rtrue; ];

    
Object ZIPI_Sonido_Fondo
  class ZIPI_Otro
  with
    ZIPI_titulo [;
      if (haySonidoFondo) "Sonido de fondo: SÍ (recomendado)";
      else                "Sonido de fondo: NO";
    ],
    ZIPI_cambiar [;
      haySonidoFondo = ~~haySonidoFondo;
      if (haySonidoFondo) {
        Damusix.VolumenCanal(CANAL_FONDO, VOLUMEN_FONDO);
      } else {
        Damusix.VolumenCanal(CANAL_FONDO, 0);
      } 
    ],
    ZIPI_ejecutar [; rtrue; ];


Object ZIPI_Accesibilidad
  class ZIPI_Menu
  with
    ZIPI_titulo "Opciones de accesibilidad",
    ZIPI_item
      ZIPI_Tiempo_Real;

    
Object ZIPI_Tiempo_Real
  class ZIPI_Otro
  with
    ZIPI_titulo [;
      if (hayTiempoReal) "Tiempo real: SÍ (recomendado)";
      else               "Tiempo real: NO";
    ],
    ZIPI_cambiar [;
      hayTiempoReal = ~~hayTiempoReal;
    ],
    ZIPI_ejecutar [; rtrue; ];

    
Object ZIPI_Jugar_Con_Intro
  class ZIPI_Otro
  with
    ZIPI_titulo "Comenzar la aventura con introducción",
    ZIPI_ejecutar [;
      StatusLineHeight(1);
      glk($002A, gg_statuswin);  ! window_clear
      glk($002F, gg_mainwin);    ! set_window
      if (hayGraficos) EncenderGraficos();
      else             ApagarGraficos();
      clearTextWindow();
      Introduccion();
      IniciarAventura();
      dibujarEstado = true;
      return 2;
    ];

    
Object ZIPI_Jugar_Sin_Intro
  class ZIPI_Otro
  with
    ZIPI_titulo "Comenzar la aventura saltando la introducción",
    ZIPI_ejecutar [;
      StatusLineHeight(1);
      glk($002A, gg_statuswin);  ! window_clear
      glk($002F, gg_mainwin);    ! set_window
      if (hayGraficos) EncenderGraficos();
      else             ApagarGraficos();
      clearTextWindow();
      IniciarAventura(true);
      dibujarEstado = true;
      return 2;
    ];

    
Object ZIPI_Restaurar
  class ZIPI_Otro
  with
    ZIPI_titulo "Restaurar una partida guardada",
    ZIPI_ejecutar [ res fref;
      StatusLineHeight(1);
      clearTextWindow();
      glk($002F, gg_mainwin);    ! set_window
      ImprimirPrompt();
      fref = glk($0062, $01, $02, 0); ! fileref_create_by_prompt
      if (fref == 0) jump RFailed;
      gg_savestr = glk($0042, fref, $02, GG_SAVESTR_ROCK); ! stream_open_file
      glk($0063, fref); ! fileref_destroy
      if (gg_savestr == 0) jump RFailed;

      @restore gg_savestr res;

      glk($0044, gg_savestr, 0); ! stream_close
      gg_savestr = 0;

    .RFailed;
      glk($002F, gg_statuswin);
      StatusLineHeight(24);
      print "^Error: no se ha podido cargar la partida.";
      rfalse;
    ];


Object ZIPI_Salir
  class ZIPI_Otro
  with
    ZIPI_titulo "Salir",
    ZIPI_ejecutar [;
      quit;
    ];


!===============================================================================
! (9) Otras Rutinas Reemplazadas; Rutinas Propias del Juego
!-------------------------------------------------------------------------------


[ BanderaFin df;
  deadflag = df;
  ControlTimer.DesactivarTick();
  Damusix.PararTodo();
];


[ FinPrologo;
  print "Te acercas, paso a paso, hasta la puerta de tu habitación, mojándote
         en el proceso las plantas de los pies, lo que te provoca una
         reacción al frío más allá de lo esperable. Apoyas la mano en el
         goteante pomo de la puerta, y el frío recorre ahora los dedos y el
         brazo en dirección a tu sobresaltado corazón. Sabes que tienes que
         abrir, algo te llama... algo tan frío y básico como el agua que
         se desliza ahora por tu piel, envolviéndote. Y, sin saber bien cómo,
         te ves girando el pomo y abriendo la puerta de par en par...";
  KeyDelay(4000);
  print "^^... sólo para descubrir ", (s_emph) "el horror", ".";
  KeyDelay(2000);
  print "^^Tu boca se abre tanto como puede para gritar y gritar y no dejar
         de gritar. Tu rostro, desencajado por el puro terror, se deforma y
         se funde como en contacto con el más poderoso ácido. Y tu corazón se
         detiene justo instantes después de verlo. Sí: de verlo. Porque antes
         de morir, lo ves.";
  KeyDelay(3000);
  print "^^Ves ", (s_user1) "el círculo.";
  KeyDelay();
  BanderaFin(2);
  clearMainWindow();
  Damusix.TocarV(Mujer_Gritando_ogg);
  MostrarImagenGradualmente(Logo_Circulo_png, 40, 20);
  MostrarImagenGradualmente(Logo_Circulo_Solo_Letras_png, 40, 20);
  print "(c) 2012 Alpha Aventuras";
  WaitDelay(1000);
];


[ HablaKatie msg nl;
  HablaPSI("TÚ:", msg, nl);
];


[ HablaBecca msg nl;
  HablaPSI("BECCA:", msg, nl);
];


[ HablaPSI psi msg nl;
  print "^", (s_user1) psi, " ~", (string) msg, "~";
  if (~~nl) new_line;
];


[ MostrarImagenGradualmente imagen veces retardo
  i;
  for (i = 0 : i < veces : i++) {
    viewImageCenter(imagen, true);
    KeyDelay(retardo);
  }  
];


[ ImprimirPrompt;
  print (s_input) "^> ";
];


[ hueles_ x;
  if (player provides persona) {
    switch (player.persona) {
      PRIMERA_PERSONA,
      PRIMERA_PERSONA_SINGULAR: print "huelo";
      SEGUNDA_PERSONA,
      SEGUNDA_PERSONA_SINGULAR: print "hueles";
      TERCERA_PERSONA,
      TERCERA_PERSONA_SINGULAR: print "huele";
      PRIMERA_PERSONA_PLURAL:   print "olemos";
      SEGUNDA_PERSONA_PLURAL:   print "oléis";
      TERCERA_PERSONA_PLURAL:   print "huelen";
      default:                  print "hueles";
    }
  } else print "hueles";
  print (string) x;
];


[ IniciarGraficosSonidos;
  if (~~testGraphics()) {
    print "^Lo sentimos, pero este juego necesita un intérprete Glulx
            con capacidades gráficas.^^";
    RecomiendaGargoyle();
  }

  if (Damusix.TestAudio() == 0) {
    print "^Lo sentimos, pero este juego necesita un intérprete Glulx
            que pueda reproducir sonido.^^";
    RecomiendaGargoyle();
  }

  if (glk_gestalt(gestalt_Timer,0) == 0) {
    print "^Lo sentimos, pero este juego necesita un intérprete Glulx
            que tenga soporte para eventos temporizados.^^";
    RecomiendaGargoyle();
  }

  ! Inicialización sonora
  IniciarSonidos();

  ! Inicialización gráfica
  initializeSGW(altoVentanaGrafica);
];


[ RecomiendaGargoyle;
  print "~El círculo~ ha sido diseñada
         para funcionar con el intérprete Gargoyle, que puedes
         descargar de la siguiente dirección:^^",
         (s_pref) "http://code.google.com/p/garglk/downloads^";
  KeyDelay();
  quit;
];


[ IniciarSonidos;
  Damusix.VolumenCanal(CANAL_FONDO, VOLUMEN_FONDO);
  Damusix.VolumenCanal(CANAL_INCIDENTAL, VOLUMEN_INCIDENTAL);
  Damusix.VolumenCanal(CANAL_TV_SALON, VOLUMEN_TV_SALON);
  Damusix.VolumenCanal(CANAL_TV_CUARTO, VOLUMEN_TV_CUARTO);
  Damusix.VolumenCanal(CANAL_TELEFONO, VOLUMEN_TELEFONO);
];

[ ComoHablar;
  print_ret "[Para hablar con un personaje, usa la forma ~",
            (s_input) "PERSONAJE, lo que sea", "~. Por ejemplo: ~",
            (s_input) "JASON, QUÉ OPINAS DE MADRE", "~.]";
];


[ Introduccion;
  clearMainWindow();
  viewImageCenter(Oscuridad_jpg);
  
  InicioPrologo();
];


[ InicioPrologo
  esp v;

  v = Damusix.QueVolumenCanal(CANAL_FONDO);
  Damusix.AsignarCanal(Grillos_ogg, CANAL_FONDO, 0, SONIDO_BUCLE_INFINITO);
  Damusix.TocarCanal(CANAL_FONDO);
  Damusix.SimpleFadeIn(Grillos_ogg, 2000, 50);

  if (hayGraficos) {
    glk_window_close(gg_bigwin, 0);
    gg_bigwin = glk_window_open(gg_mainwin,
                                winmethod_Above + winmethod_Proportional, 100,
                                wintype_Graphics, GG_BIGWIN_ROCK);
    MostrarImagenGradualmente(Prologo_png, 30, 10);
    KeyDelay(300);
    MostrarImagenGradualmente(Fundido_png, 40, 5);
  } else {
    print "Prólogo";
  }
  
  openGraphicWindow(altoVentanaGrafica);

  esp = 1000;
  print "Condado de Eola.";
  KeyDelay(esp / 2);
  print " Diez menos cuarto de la noche.";
  KeyDelay(esp);
  print "^^Dos jóvenes, dos amigas, solas, disfrutando de una noche de chicas
         en casa de una de ellas aprovechando que sus padres no están. Una es
         Becca Kotler, y la otra eres tú: Katie Embry. Solas, en tu casa,
         donde reina un silencio ahogado, tan sólo roto por el cantar de los
         grillos en el exterior y el sonido del programa de televisión que
         estáis viendo en tu habitación.";
!  print "^^Tú, Katie Embry, una adolescente, pasando la noche en compañía de
!         tu amiga y compañera de clase, Becca Kotler. Solas, en tu casa,
!         disfrutando de una noche de chicas aprovechando que tus padres no
!         están. Reina el silencio, tan sólo roto por el sonido del programa de
!         televisión que estáis viendo en tu habitación.";
  Damusix.SimpleFadeOut(Grillos_ogg, 2000, 10);
  Damusix.AsignarCanal(TV_Fondo_ogg, CANAL_TV_SALON, 0);
  Damusix.TocarCanal(CANAL_TV_SALON);
  Damusix.SimpleFadeIn(TV_Fondo_ogg, 2000, VOLUMEN_TV_SALON);
  KeyDelay(esp * 2);
  new_line;
  HablaBecca("La tele es un rollo. Toma: pon lo que quieras.");
  print "^Becca te da el mando del televisor.";
  KeyDelay(esp);
  print " Nada más cogerlo, apagas la tele y tiras el mando lejos de ti,
         cayendo por el lado contrario de la cama.^";
  Damusix.PararCanal(CANAL_TV_SALON);
  Damusix.TocarV(TV_Apaga_ogg);
  HablaKatie("No hay nada que me guste.", true);
  print " -comentas, aburrida. Y tras unos instantes jugando con un bucle de
         tu pelo, continúas:^";
  KeyDelay(esp);
  HablaKatie("Dicen que hay tantas ondas magnéticas a nuestro alrededor que
              perdemos como veinte veces más neuronas de lo normal... El
              gobierno lo sabe pero no hacen nada. Es una consipiración.");
  KeyDelay(esp);
  HablaBecca("Sé algo mejor: ¿Sabes lo de la cinta?");
  KeyDelay(esp);
  HablaKatie("¿Qué cinta?");
  KeyDelay(esp);
  print "^Becca se incorpora y te mira mientras empieza a contar:^"; 
  HablaBecca("Una cinta de vídeo, que parece normal... Se alquila, creo,
              no sé... Pero cuando la pones...");
  KeyDelay(esp);
  print "^Ahora eres tú la que se incorpora, con expresión rígida:^";
  HablaKatie("¿Qué? ¿Qué pasa?");
  KeyDelay(esp);
  print "^Becca, con expresión de divertida malicia, continúa su historia:^";
  print (s_user1) "^BECCA: ", "~Es como una pesadilla...";
  KeyDelay(esp);
  print " Y luego ves a una mujer... Y la mujer te sonríe...";
  KeyDelay(esp);
  print (s_emph) " Te ve a través de la pantalla...~";
  KeyDelay(esp);
  print "^^Tus ojos reflejan el miedo que va creciendo en tu interior, lo que
         divierte más a Becca, que continúa:";
  print (s_user1) "^^BECCA: ", "~Y cuando se acaba la cinta... Suena el
        teléfono... Alguien sabe que lo has visto...";
  KeyDelay(esp);
  print " Y te dice: ";
  KeyDelay(esp);
  print (s_emph) "'Morirás en siete días'.~";
  KeyDelay(esp / 2);
  new_line;
  HablaKatie("¿Quién te lo ha contado?");
  KeyDelay(esp);
  HablaBecca("No sé, alguien debe haber...");
  print "^La interrumpes:^";
  HablaKatie("¿Quién ha sido?");
  KeyDelay(esp);
  print "^Logras preocupar a Becca:^";
  HablaBecca("¿Qué te pasa?");
  KeyDelay(esp * 2);
  HablaKatie("Yo lo he visto. Josh y yo lo vimos el viernes pasado.");
  KeyDelay(esp);
  HablaBecca("Venga ya, Katie... ¡Es un cuento! Pero... ¿no estabas con tus
              padres? ¿¿Estuviste con Josh el fin de semana??");
  new_line;
  print (s_user1) "YO: ~", "Alquilamos una cabaña en las montañas, con unos
        amigos suyos...";
  KeyDelay(esp / 2);
  print " Estaban intentando grabar el partido, la recepción era muy mala...~";
  KeyDelay(esp / 2);
  new_line;
  HablaBecca("¿De qué me estás hab...?");
  new_line;
  print (s_user1) "YO: ~", "¡Escúchame! Cuando pusimos la cinta, el partido
        no estaba... Era... era...~";
  KeyDelay(esp / 2);
  new_line;
  HablaBecca("¿Qué? ¿Qué era?");
  new_line;
  print (s_user1) "YO: ", "~Era... ", (s_emph) "otra cosa...";
  KeyDelay(esp / 2);
  print " Pensamos que era una broma, pero...";
  KeyDelay(esp / 2);
  print " sonó el teléfono...";
  KeyDelay(esp / 2);
  print " Fue hace una semana.~";
  KeyDelay(esp);
  print "^^La miras a los ojos, llena de terror, y su preocupación hace crecer
         tu miedo:^^";
  print (s_user1) "YO: ", "~", (s_emph) "El plazo acaba hoy.", "~";
  print "^^";

  if (v ~= 0) {
    glk($00D6, 500);                      ! request_timer_events
    glk($00D2, gg_mainwin);               ! glk_request_char_event(gg_mainwin);
    while (1) {
      glk($00C0, gg_arguments);           ! glk_select(gg_arguments);
      switch (gg_arguments-->0) {
        evtype_CharInput:
          glk($00D3, gg_mainwin);         ! cancel_char_event
!          Damusix.SimpleFadeOut(Intro_ogg, 3000);
!          Damusix.PararCanal(CANAL_FONDO);
!          Damusix.VolumenCanal(CANAL_FONDO, v);
          jump Exit;

        evtype_Timer:
          glk($00D6, 500);                ! request_timer_events

        evtype_SoundNotify:
          glk($00D6, 0);                  ! request_timer_events
          jump Exit;
      }
    }
  }
.Exit;
  rfalse;
];


![ Bandera_Fin bf;
!  rfalse;
!];


#ifndef LugarReal;
[ LugarReal;
  if (location == thedark) return real_location;
  else                     return location;
];
#endif;


[ EncenderGraficos t;
  hayGraficos = true;
  openGraphicWindow(altoVentanaGrafica);
  clearGraphicWindow();
  if (location ~= thedark) {
    if (LugarReal() provides sgw_img) viewImageCenter(LugarReal().sgw_img);
    else                              viewImageCenter(Logo_Alpha_Aventuras_jpg);
  }
  if (t) {
    glk_set_style(style_Emphasized);
    print "[Gráficos activados (tamaño ";
    switch (altoVentanaGrafica) {
      GRAFICOS_PEQUENOS: print "pequeño";
      GRAFICOS_MEDIANOS: print "mediano";
      GRAFICOS_GRANDES:  print "grande";
      default:           print altoVentanaGrafica, " px";
    }
    print ").]^";
    style roman;
  }
];


[ ApagarGraficos t;
  hayGraficos = false;
  closeGraphicWindow();
  if (t) print (s_emph) "[Gráficos desactivados.]^";
];


!===============================================================================
! (10) Verbos y Gramaticas Propias del Juego
!-------------------------------------------------------------------------------


#ifdef DEBUG;
Verb 'pepe'
  *           -> Pepe;
  
[ PepeSub;
!  PNJ_Ruta(becca, MOVIMIENTO_NINGUNO);
!  ChangePlayer(becca, 1);
!  PNJ_Ruta(katie, MOVIMIENTO_PERSEGUIR, player);
!  <<Look>>;
  FinPrologo();
];
#endif;


Verb meta 'graficos'
  *                           -> Graficos
  * 'on'/'encendido'/'si'     -> EncenderGraficos
  * 'off'/'apagado'/'no'/'nx' -> ApagarGraficos
  * 'pequenos'                -> GraficosPequenos
  * 'medianos'                -> GraficosMedianos
  * 'grandes'                 -> GraficosGrandes;

Verb meta 'configuracion'
  *                           -> Configuracion;

Verb meta 'sonido' 'sonidos'
  *                                   -> Sonido
  * 'on'/'encendido'/'si'             -> EncenderSonido
  * 'off'/'apagado'/'no'/'nx'         -> ApagarSonido
  * 'fondo'                           -> Musica
  * 'fondo' 'on'/'encendido'/'si'     -> EncenderMusica
  * 'fondo' 'off'/'apagado'/'no'/'nx' -> ApagarMusica;

Verb meta 'musica'
  *                                   -> Musica
  * 'fondo' 'on'/'encendida'/'si'     -> EncenderMusica
  * 'fondo' 'off'/'apagada'/'no'/'nx' -> ApagarMusica
  * 'on'/'encendida'/'si'             -> EncenderMusica
  * 'off'/'apagada'/'no'/ 'nx'        -> ApagarMusica;

Verb meta 'pistas'
  *                 -> Pistas
  * 'no' / 'nx'     -> PistasNo;

Verb 'ayuda'
  *                 -> Ayuda;

Verb meta 'creditos'
  *                 -> Creditos;

Verb 'descuelga'
  * noun            -> Descolgar;

VerboIrregular "descolgar" with imperativo 'descuelga';

Verb 'cuelga'
  * noun            -> Colgar;

VerboIrregular "colgar" with imperativo 'cuelga';

Extend only 'enchufa' replace
  * noun            -> Enchufar;

Verb 'desenchufa'
  * noun            -> Desenchufar;

VerboIrregular "desenchufar" with imperativo 'desenchufa';

Extend 'grita' first
  *                 -> Gritar
  * 'a//' creature  -> Gritar;

Verb 'chilla' = 'grita';

Verb 'llama'
  *                 -> Llamar
  * 'a//' creature  -> Llamar
  * 'por' noun      -> LlamarPor;

Extend 'cambia' first
  * 'canal'/'emisora'                -> CambiarCanal
  * 'el' 'canal'                     -> CambiarCanal
  * 'la' 'emisora'                   -> CambiarCanal
  * 'de' 'canal'/'emisora'           -> CambiarCanal
  * 'canal'/'emisora' 'en' noun      -> CambiarCanal
  * 'el' 'canal'   'en' noun         -> CambiarCanal
  * 'la' 'emisora'  'en' noun        -> CambiarCanal
  * 'de' 'canal'/'emisora' 'en' noun -> CambiarCanal;


!===============================================================================
! (11) Rutinas de Acciones Propias del Juego
!-------------------------------------------------------------------------------


[ ConfiguracionSub
  fondo v m;
  ControlTimer.PausarTick();
  if (hayGraficos) {
    ApagarGraficos(true);
    hayGraficos = true;
  }
  fondo = Damusix.SonandoDeFondoCanal(CANAL_FONDO);
  barra_estado.numero_lineas = 26;
  barra_estado.dibujar();
  clearMainWindow();
  
  ZIPI_RunMenu(ZIPI_Configuracion, true);
  
  v = Damusix.QueVolumenCanal(CANAL_FONDO);
  if (LugarReal() provides sgw_mus) {
    m = LugarReal().sgw_mus;
    if (fondo && haySonidoFondo && v ~= 0 && ~~(Damusix.SonandoDeFondo(m))) {
      Damusix.AsignarCanal(m, CANAL_FONDO, v, SONIDO_BUCLE_INFINITO);
      Damusix.TocarCanal(CANAL_FONDO);
    }
  }
  barra_estado.numero_lineas = 1;
  barra_estado.dibujar();
  if (hayGraficos) EncenderGraficos(true);
  clearTextWindow();
  ControlTimer.ReanudarTick();
  <<Look>>;
];


[ GraficosSub;
  print (s_emph) "[Por favor, usa la forma ~", (s_input) "GRAFICOS SI",
        (s_emph) "~ o ~", (s_input) "GRAFICOS NO", (s_emph) "~ para activar o
        desactivar, respectivamente, el uso de gráficos. También puedes
        cambiar el tamaño de la ventana gráfica usando ~",
        (s_input) "GRAFICOS PEQUEÑOS", (s_emph) "~, ~",
        (s_input) "GRAFICOS MEDIANOS", (s_emph) "~ o ~",
        (s_input) "GRAFICOS GRANDES", (s_emph) "~.]^";
];


[ GraficosPequenosSub;
  altoVentanaGrafica = GRAFICOS_PEQUENOS;
  EncenderGraficosSub();
];

[ GraficosMedianosSub;
  altoVentanaGrafica = GRAFICOS_MEDIANOS;
  EncenderGraficosSub();
];


[ GraficosGrandesSub;
  altoVentanaGrafica = GRAFICOS_GRANDES;
  EncenderGraficosSub();
];


[ EncenderGraficosSub;
  EncenderGraficos(true);
];


[ ApagarGraficosSub;
  ApagarGraficos(true);
];


[ SonidoSub;
  print (s_emph) "[Por favor, usa la forma ~", (s_input) "SONIDO SI",
        (s_emph) "~ o ~", (s_input) "SONIDO NO",
        (s_emph) "~ para activar o desactivar, respectivamente,
                  todos los efectos sonoros.]^";
];


[ MusicaSub;
  print (s_emph) "[Por favor, usa la forma ~", (s_input) "SONIDO FONDO SI",
        (s_emph) "~ o ~", (s_input) "SONIDO FONDO NO", (s_emph) "~ para activar
        o desactivar, respectivamente, el sonido de fondo.]^";
];


[ EncenderSonidoSub;
  haySonido = true;
  Damusix.ActivarAudio();
  print (s_emph) "[Activados todos los efectos sonoros.]^";
];


[ ApagarSonidoSub;
  haySonido = false;
  Damusix.DesactivarAudio();
  print (s_emph) "[Desactivados todos los efectos sonoros.]^";
];


[ EncenderMusicaSub i;
  haySonidoFondo = true;
  Damusix.VolumenCanal(CANAL_FONDO, VOLUMEN_FONDO);

  if (~~i) {
    print (s_emph) "[Activado el sonido de fondo.]^";
  }
];

  
[ ApagarMusicaSub;
  haySonidoFondo = false;
  Damusix.VolumenCanal(CANAL_FONDO, 0);
  print (s_emph) "[Desactivado el sonido de fondo.]^";
];


[ PistasSub
  v tlf tv fondo;
  switch (hayPistas) {
    0: hayPistas = 1;
       print (s_emph) "[Advertencia: Es sabido que la tentación de la ayuda es
             a veces tan fuerte que se suelen obtener pistas prematuramente.
             Por tanto, en cualquier momento durante el juego puedes poner ",
             (s_input) "PISTAS NO", (s_emph) ", y eso te impedirá obtener ayuda
             en la sesión de juego actual. Si sigues queriendo una pista, pon ",
             (s_input) "PISTAS", (s_emph) " otra vez.]^";
    1: ControlTimer.PausarTick();
       if (hayGraficos) {
         ApagarGraficos(true);
         hayGraficos = true;
       }
       barra_estado.numero_lineas = 26;
       barra_estado.dibujar();
       tlf = Damusix.SonandoDeFondoCanal(CANAL_TELEFONO);
       Damusix.PararCanal(CANAL_TELEFONO);
       tv = Damusix.SonandoDeFondoCanal(CANAL_TV_SALON);
       Damusix.PararCanal(CANAL_TV_SALON);
       fondo = Damusix.SonandoDeFondoCanal(CANAL_FONDO);
       v = Damusix.QueVolumenCanal(CANAL_FONDO);
       Damusix.AsignarCanal(Darkwalk_ogg, CANAL_FONDO, v,
                            SONIDO_BUCLE_INFINITO);
       Damusix.TocarCanal(CANAL_FONDO);
       
       ZIPI_Empezar();
       
       if (tlf) Damusix.TocarCanal(CANAL_TELEFONO);
       if (tv)  Damusix.TocarCanal(CANAL_TV_SALON);
       if (fondo && LugarReal() provides sgw_mus) {
         v = Damusix.QueVolumenCanal(CANAL_FONDO);
         Damusix.AsignarCanal(LugarReal().sgw_mus, CANAL_FONDO, v,
                              SONIDO_BUCLE_INFINITO);
         Damusix.TocarCanal(CANAL_FONDO);
       } else {
         Damusix.PararCanal(CANAL_FONDO);
       }
       barra_estado.numero_lineas = 1;
       barra_estado.dibujar();
       if (hayGraficos) EncenderGraficos(true);
       clearTextWindow();
       ControlTimer.ReanudarTick();
       <<Look>>;
    2: print (s_emph) "[Las pistas han sido desactivadas.]^";
  }
];


[ PistasNoSub;
  switch (hayPistas) {
    2:       print (s_emph) "[Las pistas ya han sido desactivadas.]^";
    default: hayPistas = 2;
             print (s_emph) "[PISTAS desactivadas.]^";
  }
];


[ AyudaSub;
  print "Prueba a pedir ayuda a alguien. Si necesitas pistas más elaboradas,
         puedes usar el sistema de pistas interactivas tecleando el comando ",
         (s_input) "PISTAS", ".^";
];


[ CreditosSub;
  viewImageCenter(Logo_Alpha_Aventuras_jpg);
  print (s_head) "^* EL CÍRCULO *^^";
  print "(c) 2012 Alpha Aventuras^^";
  print (s_bold) "- PROGRAMACIÓN Y DISEÑO:^";
  print "    Ricardo Pérez López^^";
  print (s_bold) "- GRÁFICOS:^";
  print "    Manuel Millán Ruiz^^";
  print (s_bold) "- TEXTOS:^";
  print "    Ricardo Pérez López^^";
  print (s_bold) "- MAPEADO Y DOCUMENTACIÓN:^";
  print "    Ricardo Pérez López^";
  print "    Antonio Matiola Ortiz^^";
  print (s_bold) "- PRUEBAS:^";
  print "    Ricardo Pérez López^";
  print "    Manuel Millán Ruiz^^";
  KeyDelay();
  print "Desarrollado usando el lenguaje Inform, de Graham Nelson,
         con ayuda de las siguientes librerías:^^";
  print "- INFSP6 (fix), de Sarganar et al.^";
  print "- Adaptación de SGW+DMX, de Eliuk Blau^";
  print "- Damusix, de Eliuk Blau^";
  print "- PNJactor_NG y PNJacciones_NG (adaptaciones de los originales de Zak
           y Carlos)^";
  print "- Rastros_NG (adaptación de Rastros, de Mel Hython)^";
  print "- Moviles_NG (adaptación de Moviles, de Mel Hython)^";
  print "- PNJpuertas_NG (Adaptación de PnjPuertas, de Jaevius)^";
  print "- Decir, de Zak^";
  print "- Etemas, de Zak^";
  print "- Decorado_NG (adaptación de Decorado, de Zak y Mel Hython)^";
  print "- Teletipo (adaptación de Escr, de Baltasarq)^";
  print "- Adaptación de Barra, de Presi^";
  print "- Adaptación de ZIPI, de Zak^";
  print "- Timer, de Sothoth";
  KeyDelay();
  print "^^Basado en la novela ~Ringu~ de Koji Suzuki y la película ~The Ring~
         de Gore Verbinsky.";
  print "^^Nuestro agradecimiento a todos los integrantes del CAAD que nos han
         ayudado, directa o indirectamente, en el desarrollo de esta aventura.^^
         (c) 2012 Alpha Aventuras^^
         Este programa es software libre, publicado bajo la licencia GNU GPL v3.
         El texto completo de la licencia se encuentra en la siguiente
         dirección:^^";
  print (s_pref) "http://www.gnu.org/licenses/gpl.txt^";
  print "^El código fuente del programa está disponible en Github:^^";
  print (s_pref) "https://github.com/ricpelo/alien/tree/deluxe^";
  KeyDelay();
  print "^Visítanos en:^^";
  print (s_pref) "http://www.alpha-aventuras.es^
        http://wiki.caad.es/Alpha_Aventuras^
        http://www.facebook.com/alphaaventuras^";
  KeyDelay();
  viewImageCenter(LugarReal().sgw_img);
];


[ DescolgarSub;
  "No tiene sentido descolgar eso.";
];


[ ColgarSub;
  "No tiene sentido colgar eso.";
];


[ EnchufarSub;
  if (~~(noun provides enchufado)) {
    print_ret (The) noun, " no puede enchufarse.";
  }
  if (noun.enchufado) {
    print_ret (The) noun, " ya está enchufado.";
  }
  noun.enchufado = true;
  "Enchufas ", (the) noun, ".";
];


[ DesenchufarSub;
  if (~~(noun provides enchufado)) {
    print_ret (The) noun, " no puede desenchufarse.";
  }
  if (~~(noun.enchufado)) {
    print_ret (The) noun, " ya está desenchufado.";
  }
  noun.enchufado = false;
  print "Desenchufas ", (the) noun, ".";
  if (noun has on) {
    give noun ~on;
    " Al hacerlo, se apaga.";
  }
];


[ SearchSub;
  <<Examine noun>>;
];


[ ConsultSub;
  <<Examine noun>>;
];


[ TellSub;
  ComoHablar();
];


[ AnswerSub;
  ComoHablar();
];


[ GritarSub;
  "Emites un sonoro grito, pero nadie responde.";
];


[ LlamarSub;
  "¿Llamar a qué o a quién?";
];


[ LlamarPorSub;
  "Con eso no puedes llamar a ningún sitio.";
];


[ CambiarCanalSub
  tv;
  if (noun ~= nothing)                     tv = noun;
  else if (tv_cuarto_katie in LugarReal()) tv = tv_cuarto_katie;
  else if (tv_salon_katie in LugarReal())  tv = tv_salon_katie;
  else "No veo ningún televisor aquí.";
  
  if (tv hasnt on) "El televisor está apagado.";
  
  "Cambias repetidamente de canal, pero sólo encuentras estática y ruido."; 
];


! Fin del código
