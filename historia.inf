!
! EL CÍRCULO
!
! Copyright (c) 2012 Ricardo Pérez López (Alpha Aventuras)
!

! This program is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! This program is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.


!===============================================================================
! (7) Clases del Juego
!-------------------------------------------------------------------------------


Class ZonaMovimientoRuth;


!===============================================================================
! (8) Objetos del Juego
!-------------------------------------------------------------------------------


! ====================================================================
! HABITACIONES Y DECORADOS
! ====================================================================


Object cocina_ruth "Cocina"
  class Lugar
  class ZonaMovimientoRuth
  with
    name 'cocina',
    nombre_direccion "la cocina",
    description "La cocina de la casa.",
    s_to salon_ruth,
    u_to pasillo_ruth;


Object cuarto_katie_ruth "Habitación de Katie"
  class Lugar
  with
    name 'cuarto' 'katie',
    name_f 'habitacion',
    nombre_direccion "la habitación de Katie",
    description "La habitación de Katie.",
    out_to pasillo_ruth;


Object fuera_casa_ruth "Exteriores de la casa de Ruth"
  class Lugar
  with
    name 'exterior',
    nombre_direccion "el exterior de la casa",
    description "Estas fuera de la casa de Ruth y Katie.",
    in_to salon_ruth;


Object jardin_ruth "Jardín de la casa"
  class Lugar
  with
    name 'jardin',
    name_f 'terraza',
    nombre_direccion "el jardín",
    description "El jardín de la casa.",
    in_to salon_ruth;
    

Object pasillo_ruth "Pasillo superior"
  class Lugar
  with
    name 'pasillo',
    nombre_direccion "el pasillo superior",
    description "El pasillo superior.",
    d_to cocina_ruth,
    in_to cuarto_katie_ruth;


Object salon_ruth "Salón de la casa"
  class Lugar
  class ZonaMovimientoRuth
  with
    name 'salon' 'salita',
    nombre_direccion "el salón",
    description "El salón de la casa de Ruth y Katie.",
    out_to fuera_casa_ruth,
    e_to jardin_ruth,
    n_to cocina_ruth,
    each_turn [;
      if (aidan hasnt general && aidan in self) {
        give aidan general;
        PNJ_Ruta(aidan, MOVIMIENTO_POR_META, cuarto_katie_ruth,
                 CAMINO_CUALQUIERA);
        aidan.habla("Rachel, voy a pasear un poco por la casa.");
        aidan.movimiento();
        rtrue;
      }
    ];


! ====================================================================
! OBJETOS QUE LLEVAN LOS PERSONAJES AL EMPEZAR
! ====================================================================


Object llave_cajon_katie "llave del escritorio de Katie"
  with
    name 'llave',
    article "la",
    description "La llave que abre el escritorio de Katie.",
  has
    female;


! ====================================================================
! OTROS OBJETOS
! ====================================================================


Object escritorio_katie "escritorio" cuarto_katie_ruth
  class EscenarioAbrible
  class Contenedor
  with
    name_m  'escritorio' 'cajon',
    name_mp 'cajones',
    descripcion_real "El escritorio de Katie.",
    when_open "^El cajón del escritorio de Katie aparece abierto.",
    with_key llave_cajon_katie,
    after [;
      Unlock:
        <<Open self>>;
    ],
  has
    lockable locked;


Object carpeta_katie "carpeta" escritorio_katie
  with
    name 'carpeta',
    description "Una carpeta.",
    initial "Un ticket sobresale ligeramente por un lateral de la carpeta.",
    after [;
      Open:
        move ticket_fotos to LugarReal();
        "Al abrir la carpeta, ves lo que parece ser un ticket de algún tipo de
         establecimiento, sobresaliendo un poco por el lado.";
    ]
  has
    openable female;


Object ticket_fotos "ticket"
  with
    name 'ticket',
    adjectives 'foto' 'fotos',
    short_name "ticket",
    description "Un ticket de un establecimiento de revelado automático de fotos
                 las 24 horas.",
    after [;
      Examine:
        self.short_name = "ticket de revelado de fotos";
    ];


! ====================================================================
! PERSONAJES
! ====================================================================


Object aidan "Aidan"
  class Personaje
  with
    name 'aidan',
    short_name "Aidan",
    description "Es Aidan.",
    pnj_ha_llegado [;
      if (self.tipo_de_movimiento == MOVIMIENTO_POR_META) {
        PNJ_Ruta(self, MOVIMIENTO_NINGUNO);
      }
    ],
    life [;
      Ask:
        return self.habla("No te entiendo, Rachel.");
    ],
    orders [;
      Venir, Seguir:
        if (noun == nothing) noun = player;
        if (noun == self) return self.habla("No puedo.");
        PNJ_Ruta(self, MOVIMIENTO_PERSEGUIR, noun);
        return self.habla("Ya voy, Rachel.");

      default:
        return self.habla("No puedo hacer eso, Rachel.");
    ];


Object amiga_morena "amiga morena"
  class Personaje
  with
    short_name "Chica morena";


Object amiga_rubia "amiga rubia"
  class Personaje
  with
    short_name "Chica rubia";


Object amigo_katie "amigo"
  class Personaje
  with
    short_name "Chico";


Object amigos_katie "amigos de Katie"
  class Character
  class Personaje
  with
    name 'amigo' 'chico',
    name_f 'amiga',
    name_mp 'amigos' 'chicos',
    name_fp 'amigas',
    adjectives 'katie',
    article "a unos",
    short_name "amigos de Katie",
    gender G_MASCULINO + G_PLURAL,
    description "Tres amigos de Katie.",
    initial "Tres chicos (probablemente amigos de Katie) conversan entre
             ellos.",
    quip [ a b;
      switch (a) {
        ! Quip 1: Menú principal
        11: ! No se necesita nombre de quip para el menú principal
        12: ! No se necesita respuesta
        13: qtype = MainMenu;
            qqon = true;
        14: return Qlist(b, 2, 2, 3);
            
        ! Quip 2
        21: "Hola, chicos... ¿Cómo estáis?";
        22: self.qoff(2); self.qon(3);
            amiga_morena.habla("Bien.");
            new_line;
        23: qqon = true;
            killq = true;
            qtransfer = 1;
        
        ! Quip 3
        31: "¿Sabéis algo de Katie?";
        32: self.qoff(3);
            amiga_morena.habla("No.");
            new_line;
        33: killq = true;
            qtransfer = 1;        
      }
    ],
    life [
      tema;
      Ask:
        self.Qselect(1);
        rtrue;
    ],
    orders [;
      Saludar:
        self.Qselect(1);
        rtrue;
    ],
  has
    ~proper;


Object rachel "Rachel"
  class Personaje
  with
    name 'rachel',
    short_name "Rachel",
    description [;
      if (player == self) "Eres tú: Rachel."; 
      else "Es Rachel.";
    ],
    orders [;
      if (player == self) {
        ! Este objeto (MANOLO) es el que tiene el foco, es decir
        ! el que está siendo controlado por el jugador de carne y hueso
        if (actor == self) {
          ! Y la orden me está siendo dada a mi, por ejemplo
          ! el jugador ha puesto: ``SALTA'' mientras controla a MANOLO
        } else {
          ! La orden ha sido dada a otro personaje, desde éste
          ! Por ejemplo el jugador ha puesto ``HELENA, SALTA''
          ! mientras controla a Manolo
        }
      } else {
        ! La orden proviene de otro personaje (que está siendo controlado
        ! por el jugador ``real'') y ha sido dirigida a Manolo.
        ! Por ejemplo, el jugador ha puesto MANOLO, SALTA mientras
        ! controla a otro personaje.
      }
    ],
  has
    female;


Object ruth "Ruth"
  class Personaje
  with
    name 'ruth' 'tia',
    short_name "Ruth",
    description "Es Ruth.",
    life [
      tema old_ret;
      Ask:
        if (LugarReal() ~= cocina_ruth) {
          self.habla("Aquí no... Vamos a un sitio más tranquilo.");
          PNJ_Ruta(self, MOVIMIENTO_NINGUNO);
          PNJIr(self, n_obj);
          rtrue;
        }

        tema = AveriguarTema(TemasRuth);

        switch (tema) {
          tema_ruth_katie:
            if (self hasnt general) {
              give self general;
              old_ret = self.habla_retardo;
              self.habla_retardo = 1000;
              self.habla("No tiene sentido, Rachel...", " Me he pasado cuatro
                          horas en Internet y no he podido encontrar ni un solo
                          caso de una niña de dieciséis años a la que se le
                          pare el corazón...", " He hablado con tres médicos y
                          ninguno sabe decirme qué le pasó a Katie...");
              self.habla_retardo = old_ret;
              rtrue;
            } else {
              return self.habla("Por favor, Rachel: descubre qué le ha pasado
                                 a mi Katie...");
            }

          tema_ruth_cajon:
            if (llave_cajon_katie in self) {
              ! Ruth aún tiene la llave:
              move llave_cajon_katie to rachel;
              print "Ruth permanece un momento con la mirada distante. Instantes
                     después, mete dos dedos en un pequeño bolsillo de su
                     vestido y saca una minúscula llave plateada. La contempla
                     brevemente ante sus ojos y te la entrega.^";
              return self.habla("Toma: me guardé la llave de su escritorio
                                 ayer, cuando hice limpieza en su habitación.");
            } else {
              ! Ya te la ha dado:
              return self.habla("Espero que te sirva de ayuda.");
            }

          default:
            return self.habla("No lo sé... De verdad que no lo sé...");
        }
    ],
    orders [;
      Saludar:
        return self.habla("Hola, Rachel.");

      default:
        return self.habla("Perdona si soy brusca, pero mi hija acaba de morir y
                           no me apetece hacer nada.");
    ],
  has
    female;


! ====================================================================
! GESTORES DE TIMER
! ====================================================================


! ====================================================================
! OBJETOS DE SOPORTE
! ====================================================================


! ====================================================================
! TEMAS RAIZ
! ====================================================================


! Cuidado con los infinitivos. Si pones un infinitivo aquí, luego no se puede
! usar como verbo

Object TemasRuth;

Object tema_ruth_katie "Katie" TemasRuth
  with
    name 'katie' 'hija';

Object tema_ruth_cajon "el cajón del escritorio de Katie" TemasRuth
  with
    name 'cajon' 'escritorio' 'llave';


!===============================================================================
! (9) Otras Rutinas Reemplazadas; Rutinas Propias del Juego
!-------------------------------------------------------------------------------


[ FinHistoria;
  "Aquí acaba.";
];


[ InicioHistoria;
  Damusix.PararTodo();
  ControlTimer.Resetear();

  remove sonido_telefono_katie;  give sonido_telefono_katie  absent;
  remove sonido_tv_cuarto_katie; give sonido_tv_cuarto_katie absent;
  remove sonido_tv_salon_katie;  give sonido_tv_salon_katie  absent;
  
  clearMainWindow();
  print "^^Aquí comienza la historia.^";
  KeyDelay();

  move rachel to fuera_casa_ruth;
  PNJ_Ruta(rachel, MOVIMIENTO_NINGUNO);
  ChangePlayer(rachel);

  remove katie;

  move aidan to fuera_casa_ruth;
  PNJ_Ruta(aidan, MOVIMIENTO_PERSEGUIR, rachel);

  move llave_cajon_katie to ruth;
  move ruth to salon_ruth;
  ! Ruth se mueve por la cocina y el salón, con un 30% de probabilidad:
  PNJ_Ruta(ruth, MOVIMIENTO_ALEATORIO, 10, CAMINO_CUALQUIERA,
           ZonaMovimientoRuth);

  move amigos_katie to jardin_ruth;
  PNJ_Ruta(amigos_katie, MOVIMIENTO_NINGUNO);

  PlayerTo(fuera_casa_ruth);
];


#ifdef DEBUG;
[ TestHistoria;
  test_machine.clear();
  test_machine.inserta("entra");
  test_machine.inserta("jardin");
  test_machine.inserta("chicos, hola");
  test_machine.inserta("1");
  test_machine.inserta("1");
  test_machine.inserta("entra");
  test_machine.inserta("ruth, llave");
  test_machine.inserta("n");
  test_machine.inserta("ruth, llave");
  test_machine.inserta("sube");
  test_machine.inserta("entra");
  test_machine.inserta("abre cajon con llave");
  test_machine.inserta("coge carpeta");
  test_machine.inserta("abre carpeta");
  test_machine.inserta("x carpeta");
  test_machine.inserta("x ticket");
  test_machine.inserta("mira");
  test_machine.inserta("coge ticket");
  test_machine.inserta("i");
];
#endif;


!===============================================================================
! (10) Verbos y Gramaticas Propias del Juego
!-------------------------------------------------------------------------------


#ifdef DEBUG;
Verb meta 'i1'
  *                      -> InicioHistoria;
  
Verb meta 'testhistoria'
  *                      -> TestHistoria
  * 'full'               -> TestHistoriaFull;
#endif;

!===============================================================================
! (11) Rutinas de Acciones Propias del Juego
!-------------------------------------------------------------------------------


#ifdef DEBUG;
[ InicioHistoriaSub;
  InicioHistoria();
];


! Funciones de test. Ver TestMachine_NG.h
[ TestHistoriaFullSub;
  TestHistoria();
  test_machine.run();
];


! Funciones de test. Ver TestMachine_NG.h
[ TestHistoriaSub;
  TestHistoria();
  test_machine.run_once();
];
#endif;


! Fin historia.inf
