!===============================================================================
! (7) Clases del Juego
!-------------------------------------------------------------------------------


Class LugarCasaKatie
  class Lugar
  with
    after [ o;
      Go:
        objectloop (o ofclass Sonido && o in self) {
          o.cambiar_volumen();
        }
    ];


!===============================================================================
! (8) Objetos del Juego
!-------------------------------------------------------------------------------


! ====================================================================
! HABITACIONES Y DECORADOS
! ====================================================================

! ---------------------------------------------
! PRÓLOGO
! ---------------------------------------------


Object cuarto_katie "Tu habitación"
  class LugarCasaKatie
  with
    description "La pequeña lámpara de la mesita situada junto a tu cama
                 proyecta débiles y verdosos haces de luz que iluminan tu
                 habitación con un tono que, en estos momentos, te resulta
                 enfermizo y asfixiante. El tamaño de tu cama parece pugnar
                 por abarcar el mayor espacio posible de la estancia, sólo
                 equilibrado por el pequeño televisor que observa todo sin
                 parpadear su único ojo desde una pequeña mesa situada en
                 la pared contraria.",
    out_to pasillo_katie,
    sgw_img Oscuridad_jpg,
    compass_look [ d;
      if (d == out_obj) {
          "A través de la puerta entreabierta observas el pasillo en penumbra.";
      }
    ];


Object decorado_cuarto_katie cuarto_katie
  class Decorado
  with
    before [;
      Open:
        if (self.palabra == 'puerta') {
          "Ya estaba abierta.";
        }

      Close:
        if (self.palabra == 'puerta') {
          HablaBecca("¿Pero no vas a salir a coger el teléfono?");
          rtrue;
        }

      Take, Push, PushDir:
        if (self.palabra == 'cama') {
          "Es demasiado pesada.";
        }
    ],
    sinonimos
      'mesa' 'mesita' (-1),
    describe
      'cama' "Te gusta dormir cómoda, y por ello tu cama tiene casi el doble
              de ancho que una cama normal, pero ahora, sin tener muy claro
              el por qué, se te antoja poco acogedora." G_FEMENINO
      'lampara' "Una lámpara que emite una tenue luz especialmente verdosa." 
                G_FEMENINO
      'mesita' "Una baja mesita al fondo de la habitación sostiene,
                solitaria, el televisor del dormitorio." G_FEMENINO
      'puerta' "La puerta de tu habitación permanece entreabierta.";


Object pasillo_katie "Pasillo del piso superior"
  class LugarCasaKatie
  with
    description [;
      print "Nunca, hasta ahora, el pasillo del piso superior de tu casa
             te había parecido tan oscuro y carente de alma. Una fría
             corriente de aire lo recorre de extremo a extremo, subiendo
             por las escaleras que dan a la planta baja y cruzando tu
             espalda en escalofríos continuos y persistentes.";
      if (puerta_katie notin self) {
        " La puerta abierta a tu habitación es prácticamente la única
         fuente de luz en este lúgubre pasaje."; 
      } else {
        new_line;
      }
    ],
    in_to cuarto_katie,
    d_to cocina_katie,
    sgw_img Oscuridad_jpg;


Object decorado_pasillo_katie pasillo_katie
  class Decorado
  with
    before [;
      Open:
        "Ya estaba abierta.";

      Close:
        "Pensándolo bien, será mejor no cerrarla. Perderías la mayor parte de
         la poca luz que ilumina el pasillo.";
    ],
    describe
      'puerta' "La puerta entreabierta muestra una vista parcial de tu
                habitación, cercándola como el marco de un cuadro vertical y
                alargado."
                G_FEMENINO;

    
Object cocina_katie "Cocina"
  class LugarCasaKatie
  with
    description "La cocina, sobria y funcional, es el núcleo central del
                 piso inferior, el distribuidor por el que llegar a los
                 restantes lugares de la casa. El frigorífico y el teléfono
                 son los dos únicos elementos destacables en una estancia,
                 por otro lado, gélida y casi en penumbra. A través de un
                 enorme arco sin puerta se accede al salón de la casa.",
    u_to pasillo_katie,
    s_to salon_katie,
    sgw_img Oscuridad_jpg,
    before [;
      Go:
        if (noun == u_obj) {
          if ((~~(sonido_telefono_katie.sonando())) &&
              tv_salon_katie.enchufado) {
            switch (tv_salon_katie.encendidos_solo) {
              0: if (tv_salon_katie has on) {
                   tv_salon_katie.apagar();
                   print "Justo cuando ibas a salir de la cocina para subir al
                          piso de arriba, oyes que el televisor se apaga solo
                          en el salón. Te giras para comprobar que,
                          efectivamente, el salón está a oscuras y el silencio
                          parece ahora más intenso que nunca.";
                   KeyDelay();
                   print " Instantes después, vuelve a encenderse, dejando en
                          el ambiente un sonido de estática tan carente de
                          calidez y alma como el mismo silencio.^";
                 } else {
                   print "Justo cuando ibas a salir de la cocina para subir al
                          piso de arriba, oyes que el televisor se enciende
                          solo en el salón. Te giras lentamente para comprobar
                          que, en efecto, el salón brilla y parpadea al ritmo
                          de un sonido de estática tam carente de calidez y
                          alma como el mismo silencio.^";
                 }
                 tv_salon_katie.encender();
                 HablaKatie("Becca, deja de bromear... ¿Dónde estas?");
                 print "^Miras a tu alrededor, pero sigues estando sola.^";
                 rtrue;

              1: if (tv_salon_katie has on) {
                   jump NoDejarEncendido;
                 } else {
                   tv_salon_katie.se_enciende_por_segunda_vez(false);
                   rtrue;
                 }

              default:
                if (tv_salon_katie has on) {
              .NoDejarEncendido;
                  "Algo te dice que no quieres dejar el televisor encendido.
                   Algo inexplicable, irracional y primario.";
                }
            }
          }
        }
    ];


Object decorado_cocina_katie cocina_katie
  class Decorado
  with
    describe
      'arco' "La pared sur se abre en forma de arco para acceder al salón." 0;


Object salon_katie "Salón"
  class LugarCasaKatie
  with
    description "El salón de tu casa está claramente dominado por un televisor
                 de gran tamaño situado sobre una mesa en la pared frontal.
                 Junto al enorme sofá a unos tres metros de la misma forman
                 en la práctica el único mobiliario disponible, si exceptuamos
                 un par de sillas a ambos lados del sofá y algunos cuadros
                 colgados de las paredes. Una ventana abierta deja pasar la
                 oscuridad del exterior.",
    n_to cocina_katie,
    before [;
      Go:
        if (noun == n_obj && frigorifico_katie has open && 
            frigorifico_katie hasnt general) {
          give frigorifico_katie general;
          print "Nada más cruzar el arco que separa la cocina del salón, notas
                 algo moverse junto al teléfono, justo en el extremo de tu
                 percepción visual. Al girar la mirada, ves la causa y el
                 corazón te da un vuelco: la puerta del frigorífico se va
                 abriendo poco a poco, totalmente silenciosa, hasta quedar a
                 medio abrir.";
          KeyDelay();
          new_line;
        }
    ],
    sgw_img Oscuridad_jpg;


Object decorado_salon_katie salon_katie
  class Decorado
  with
    sinonimos
      'silla' 'sillas' G_FEMENINO,
    describe
      'sillas' "Son dos sillas, una a cada lado del sofá central."
               G_FEMENINO + G_PLURAL
      'sofa'   "Aunque sabes que resulta cómodo una vez que te sientas, al tacto
                resulta resbaladizo, frío y duro. Permanece estático en el
                centro de la estancia." 0
      'mesa'   "La mesa que sostiene el televisor tiene el tamaño justo para
                albergar un aparato de tales dimensiones." G_FEMENINO;
    
    
! ====================================================================
! OBJETOS QUE LLEVAN LOS PERSONAJES AL EMPEZAR
! ====================================================================


! ====================================================================
! OTROS OBJETOS
! ====================================================================


Object telefono_katie "teléfono" cocina_katie
  with
    name 'telefono' 'auricular' 'tlf',
    description "El teléfono de tu casa, de un color blanco gastado, cuelga de
                 la pared junto al frigorífico.",
!    initial "Ves tu teléfono.",
    sonando [;
      return sonido_telefono_katie.sonando();
    ],
    descolgar [;
      sonido_telefono_katie.parar(true);
      remove becca;
      becca_ausente.found_in = true;
      move becca_ausente to LugarReal();
      move charco_katie to pasillo_katie;
      move puerta_katie to pasillo_katie;
    ],
    becca_lo_coge [;
      self.descolgar();
      print "Becca coge el teléfono con expresión seria.^";
      HablaBecca("¿Diga?", true);
      print " -la oyes decir, casi susurrando,
            al auricular del teléfono.^^";
      print "Lentamente, a medida que escucha lo que el teléfono le dice, se
             gira hacia ti con una inclasificable mirada en los ojos, y tu
             preocupación crece por momentos...";
      KeyDelay(2000);
      print " hasta que te muestra una amplia sonrisa y pasándote el teléfono,
             divertida, te dice:^";
      HablaBecca("Es tu madre");
      new_line;
      self.coges_el_telefono();
    ],
    coges_el_telefono [;
      print "Coges el teléfono, y aún con desconfianza lo acercas a tu oído.
             Tras emitir un nervioso ~¿Hola?~, oyes al otro lado de la línea
             la tranquilizadora (y enfática) voz de tu madre preguntándote qué
             tal va la noche y exhortando que no te vayas tarde a la cama.
             Durante la corta pero extrañamente tranquilizadora conversación,
             percibes la voz de Becca preguntando algo mientras se aleja de la
             cocina escaleras arriba. Finalmente, cuelgas el teléfono...";
      KeyDelay(5000);
      print (s_emph) " y estas sola.^";
      HablaKatie("¿Becca?", true);
      print " -la llamas temiendo alzar demasiado la voz, sin respuesta.^";
      HablaKatie("¿BECCA?", true);
      print " -gritas esta vez, sacando fuerzas del miedo.^^";
      print_ret (s_emph) "No hay respuesta.";
!      print (s_user1) "¿Becca?");
    ],
    before [;
      Take, Descolgar:
        if (self.sonando()) {
          self.descolgar();
          self.coges_el_telefono();
          rtrue;
        } else {
          "Descuelgas y no oyes nada. Ni siquiera el tono de marcado.";
        }

      Drop, Colgar:
        "El teléfono ya está colgado.";

      LlamarPor:
        if (self.sonando()) {
          "El teléfono está sonando. No puedes llamar tú ahora.";
        } else {
          <<Descolgar self>>;
        }
    ],
  has
    scenery;


Object frigorifico_katie "frigorífico" cocina_katie
  class EscenarioAbrible
  with
    name_m 'frigorifico' 'congelador',
    name_f 'nevera' 'puerta',
    article "el",
    description [;
      if (self hasnt open) {
        "Un moderno frigorífico de puertas metálicas y casi imperceptible
         ronroneo.";
      } else {
        "El frigorífico, entreabierto, emite un haz de luz azulado bañando
         parte de la cocina a su paso. El sonido del motor resulta más
         audible que cuando estaba cerrado.";
      }
    ],
    ya_visto_abierto false,
    when_open [;
      if (~~(self.ya_visto_abierto)) {
        self.ya_visto_abierto = true;
        "^Ves el frigorífico abierto, como una invitación a lo desconocido."; 
      } else {
        "^El frigorífico sigue abierto.";
      }
    ],
    before [;
      Open:
        if (self hasnt general) {
          "Abres el frigorífico, observas la comida con nulo interés, y vuelves
           a cerrarlo cuestionándote brevemente el por qué de tu falta de
           apetito.";
        } else {
          "No te atreves a volverlo a abrir.";
        }

      Close:
        if (self hasnt open) "El frigorífico ya está cerrado.";
    ],
    after [;
      Close:
        self.cerrar();
        "Cierras el frigorífico con rapidez sin atreverte a mirar en su
        interior.";
    ];


Object tv_cuarto_katie "televisor" cuarto_katie
  with
    name_f 'tv' 'television' 'tele' 'pantalla',
    name_m 'televisor',
    description [;
      print "Un pequeño televisor sin aspectos destacables. Está ";
      if (self has on) print "encendido, emitiendo nieve y ruido de estática";
      else             print "apagado";
      ".";
    ],
!    initial "Ves el televisor.",
    encender [;
      give self general;
      sonido_tv_cuarto_katie.sonar();
    ],
    apagar [;
      sonido_tv_cuarto_katie.parar();
    ],
    before [;
      Examine:
        PrintOrRun(self, description);
        rtrue;

      Enchufar:
        "El televisor ya estaba enchufado.";
        
      Desenchufar:
        "¿Para qué quieres desenchufarlo?";
    ],
    after [ c;
      SwitchOn:
        c = self hasnt general;
        self.encender();
        if (c) {
          "Al encender el televisor, te sorprende comprobar que no aparece
           ninguna imagen, sino simple ruido y estática, como si no recibiera
           señal.";
        } else {
          "Vuelves a encender el televisor y el ruido granulado aparece de
           nuevo en la pantalla acompañado de estática.";
        }

      SwitchOff:
        self.apagar();
        "Apagas el televisor, sin evitar sentir cierto alivio al hacerlo.";
    ],
  has
    scenery switchable;


Object tv_salon_katie "televisor" salon_katie
  with
    name_f 'tv' 'television' 'tele' 'pantalla',
    name_m 'televisor',
    description [;
      if (self hasnt general) {
        "Un televisor negro, impersonal, de gran tamaño, con una pantalla
         tan oscura que su superficie apenas refleja el entorno en el que
         se encuentra.";
      } else {
        if (self has on) {
          "El enorme televisor ilumina la estancia con un brillo vivo y
           cambiante, resultado del perpetuo movimiento del ruido en la
           pantalla, que te recuerda al de millones de organismos
           microscópicos moviéndose y chocando entre sí.";
        } else {
          "El televisor, ahora apagado, se presenta tan oscuro como un mal
           presagio.";
        }
      }
    ],
    describe [;
      if (self has on) {
        "^El televisor está encendido, mostrando un ruido granulado que baña
         el salón con una luz sucia y nerviosa.";
      }
      rtrue;
    ],
!    initial "Ves el televisor.",
    time_left,
    time_out [;
      if (self hasnt on) {
        self.se_enciende_por_segunda_vez(true);
!      "^Todos los músculos del cuerpo se te agarrotan y tus pulmones se quedan
!       sin aire cuando notas que el televisor del salón vuelve a encenderse
!       solo."
!      Se vuelve a encender el televisor.";
      }
    ],
    se_enciende_por_segunda_vez [ sw;
      ! sw == false si viene de cocina_katie al intentar subir
      ! sw == true  si viene del time_out de tv_salon_katie
      self.encender();
      Damusix.AsignarCanal(Gathering_Darkness_ogg, CANAL_INCIDENTAL,
                           VOLUMEN_INCIDENTAL, SONIDO_BUCLE_INFINITO);
      Damusix.TocarCanal(CANAL_INCIDENTAL);
      if (~~sw) {
        print "El televisor se vuelve a encender. Solo. Otra vez.
               Más sonido de estática proveniente del salón,
               transformando la atmósfera en pura inquietud. Ahora
               sí que puedes sentir con claridad los latidos de tu
               corazón rebotando en tu pecho de pura ansiedad.^";
      } else {
        if (LugarReal() ~= salon_katie) {
          print "^Un momento... ¿Eso que oyes... no es el televisor?^";
        }
        print "^Durante unos instantes no acabas de creerlo... Buscas una
               explicación lógica, intentas convencerte a ti misma de que no
               es cierto... Pero la realidad, injusta pero imparcial, te lo
               deja claro: el televisor del salón ha vuelto a encenderse
               solo.^";
      }
      HablaKatie("¡Becca, vale ya!");
      "^Pero Becca no está. Y te quedas ahí, sin subir la escalera, mirando
       en dirección al salón y el televisor encendido.";
    ],
    enchufado true,
    encendidos_solo 0,
    encender [ sw;
      give self general;
      give self on;
      sonido_tv_salon_katie.sonar();
      if (~~sw) self.encendidos_solo++;
    ],
    apagar [;
      give self ~on;
      sonido_tv_salon_katie.parar();
    ],
    before [;
      Examine:
        PrintOrRun(self, description);
        rtrue;

      SwitchOn:
!        if (~~(self.enchufado))
!          "No se puede encender porque está desenchufado.";
        if (self has on) "Ya está encendido.";
        rfalse;
        
      SwitchOff:
!        if (~~(self.enchufado))
!          "Ya está desenchufado.";
        if (self hasnt on) "Ya está apagado.";
        rfalse;

      Enchufar:
        "El televisor ya está enchufado.";
        
      Desenchufar:
        "El cable del enchufe pasa por la parte trasera y no alcanzas a tirar
         de él.";
        
!      Tie:
!        if (self.enchufado) {
!          "El televisor ya está enchufado.";
!        } else {
!          self.enchufado = true;
!          "Enchufas el televisor.";
!        }

!      Desenchufar:
!        if (~~(self.enchufado)) {
!          "El televisor ya está desenchufado.";
!        } else {
!          self.enchufado = false;
!          if (self has on) {
!            self.apagar();
!            "Desenchufas el televisor. Al hacerlo, la pantalla se apaga.";
!          } else {
!            "Desenchufas el televisor.";
!          }
!        }     
    ],
    after [;
      SwitchOn:
        self.encender(true);
        StopTimer(self);
        if (self hasnt general) {
          give self general;
          "Al encender el televisor compruebas que no aparece ninguna emisión
           salvo la típica imagen de ruido granulado y el sonido de estática
           que se oye siempre que no se capta ninguna señal.";
        } else {
          "Vuelves a encender el televisor y éste vuelve a mostrar ruido y a
           emitir sonido de estática.";
        }

      SwitchOff:
        self.apagar();
        switch (self.encendidos_solo) {
          1: StartTimer(self, 3);
          2: frigorifico_katie.abrir();
        }
        "Con un gesto rápido, apagas el televisor y la estática desaparece,
         retornando la estancia a la semioscuridad.";
    ],
  has
    static switchable;


Object charco_katie "charco de agua"
  with
    name 'charco' 'agua' 'liquido',
    description "Es, claramente, un charco con forma irregular
                 alimentado del agua que se filtra por debajo de la puerta
                 de tu habitación.",
    initial [;
      StartTimer(self, 4);
      "Depositado en mitad del pasillo, como una mancha líquida en el
       suelo, ves un charco de agua que se extiende filtrándose por
       debajo de la puerta cerrada de tu habitación.";
    ],
    time_left 0,
    time_out [;
      self.initial = "El charco es cada vez más grande. No deja de salir agua
                      por debajo de la puerta.";
    ],
    react_before [;
      Examine:
        if (noun == d_obj) {
          <<Examine self>>;
        }
    ],
    before [;
      Touch:
        "Tus pies descalzos ya pisan el charco de agua y perciben su frío y su
         viscosidad.";

      Smell:
        "El agua expele un hedor putrefacto, podrido, como el del agua
        estancada.";
    ],
  has
    static;


Object puerta_katie "puerta de tu habitación"
  with
    name 'puerta' 'habitacion',
    article "la",
    description "Bajo la puerta cerrada de tu habitación se escurre hacia el
                 pasillo un flujo lento pero constante de agua. Un resplandor
                 blanquecino proveniente de tu habitación acompaña al río de
                 agua mientras atraviesa la rendija inferior de la puerta. Del
                 pomo, empapado como si rezumara humedad, caen continuas gotas
                 al suelo.",
    initial "Alguien ha cerrado la puerta de tu habitación, dejando bajo ella
             una fina abertura por la que salen un líquído hediondo y una
             luz mortecina.",
    add_to_scope decorado_puerta_katie,
    react_before [;
      Enter, GoIn:
        <<Open self>>;

      Examine:
        rfalse;
        
      default:
        if (noun == decorado_puerta_katie &&
          decorado_puerta_katie.palabra_usada == 'pomo' or 'picaporte') {
          noun = puerta_katie;
          ! Esto es porque al hacer el truco de la línea anterior (de cambiar
          ! el noun por la cara) luego no se ejecuta la rutina before:
          return RunRoutines(self, before);
        }
        rfalse;
    ],
    before [;
      Examine:
        PrintOrRun(self, description);
        rtrue;

      Open:
        FinPrologo();
        rtrue;
        
      Close:
        "La puerta ya está cerrada.";
        
    ],
  has
    static female openable;


Object decorado_puerta_katie
  class Decorado
  with
    before [;
      if (action ~= ##Examine) {
        if (self.palabra_usada == 'gota' or 'gotas') {
          "Las gotas de agua son casi inmateriales y se escurren entre tus
          dedos.";
        } else if (self.palabra == 'luz') {
          "No puedes tocar la luz, pero de todas formas tampoco te atreves.";
        }
      }

      Touch:
        if (self.palabra == 'pomo') {
          "Tocas el pomo mojado de la puerta y la humedad traspasa tu piel.";
        }

      Take:
        if (self.palabra_usada == 'pomo' or 'picaporte') {
          <<Open puerta_katie>>;
        }
    ],
    sinonimos
      'picaporte'  'pomo' (-1)
      'gota'       'pomo' G_FEMENINO
      'gotas'      'pomo' G_FEMENINO + G_PLURAL
      'haz'        'luz'  0
      'resplandor' 'luz'  0
      'abertura'   'luz'  (-1),
    describe
      'pomo' "Del pomo de la puerta cae un goteo continuo de agua que no
              acabas de comprender de dónde proviene." 0
      'luz'  "La luz se proyecta desde el interior de tu habitación y sale
              por debajo de la puerta proyectando un haz de un color
              enfermizo." G_FEMENINO;


! ====================================================================
! PERSONAJES
! ====================================================================


Object katie "Katie" cuarto_katie
  class Movil
  with
    name 'katie',
    description "Eres tú: Katie Embry, una chica adolescente algo alocada pero
                 responsable, de ojos oscuros y cabello largo, rubio y liso.
                 Tu mejor amiga Becca y tú estáis pasando una noche solas en tu
                 casa, aprovechando que tus padres están fuera. Aún llevas
                 puesto tu uniforme escolar, aunque vas descalza.",
  has
    female;


Object becca "Becca" cuarto_katie
  class Movil
  with
    name 'becca',
    description "Becca es, sin duda, tu mejor amiga, y lo es desde os
                 conocísteis, hace ya siglos. Es morena, con grandes ojos
                 verdes, y de carácter un tanto rebelde. Compañeras de
                 Instituto que sois, lleva puesto el mismo uniforme que tú.",
    initial "^Junto a ti está Becca.",
    describe [;
      switch (random(3)) {
        1: "^A tu lado, tu amiga Becca te observa con inquietud.";
        2: "^Becca te mira esperando qué vas a hacer.";
        3: "^Tu amiga Becca está a tu lado.";
      }
    ],
    react_before [;
      Gritar:
        HablaBecca("¡¿Pero qué te pasa?! ¿Qué ocurre? ¿A qué vienen esos
                    gritos?");
        rtrue;
    ],
    react_after [ c;
      SwitchOn:
        if (noun == tv_cuarto_katie or tv_salon_katie) {
          c = tv_cuarto_katie hasnt general && tv_salon_katie hasnt general;
          ! Lo hago así para que el mensaje de Becca aparezca DESPUÉS de los
          ! mensajes que genere la rutina after:
          RunRoutines(noun, after);
          if (c) {
            HablaBecca("Qué extraño... ¿Qué ha pasado con la imagen?");
          } else {
            HablaBecca("Sigue sin haber imagen...");
          }
          rtrue;
        }
    ],
    before [;
      Llamar:
        HablaBecca("Estoy aquí... ¿No me ves?");
        rtrue;
    ],
    life [
      tema;
      Ask:
        tema = AveriguarTema(TemasBecca);
        switch (tema) {
          tema_becca_telefono:
            HablaBecca("¿Crees que puede ser la misma persona que te llamó
                        en la cabaña?");
            PreguntaSiNo = 1;

          tema_becca_tv:
            if (tv_cuarto_katie has general || tv_salon_katie has general) {
              HablaBecca("Parece que se ha ido la señal.");
            } else {
              HablaBecca("Sí, haces bien en apagar la tele. Es un rollo.");
            }
            
          default:
            HablaBecca("No entiendo qué quieres decirme.");
        }
        rtrue;
    ],
    orders [;
      if (player == self) rfalse;
      Go, GoIn, Enter, Exit, Subir, Bajar:
        HablaBecca("No pienso ir sola a ninguna parte.");
        rtrue;
        
      Take:
        if (noun == telefono_katie) {
          if (LugarReal() == cocina_katie) {
            telefono_katie.becca_lo_coge();
            rtrue;
          } else {
            HablaBecca("¿Dónde está el teléfono?");
            rtrue;
          }
        }

      NotUnderstood, Tell:
        <<Ask self>>;

      default:
        HablaBecca("Mejor hazlo tú misma.");
        rtrue;
    ],
  has
    animate proper;


Object becca_ausente "Becca ausente"
  with
    name 'becca',
    description [;
      if (location == cocina_katie or salon_katie) {
        "No ves a Becca por ninguna parte. ¿Estará arriba?";
      } else {
        "No ves a Becca por ninguna parte. ¿Estará en tu habitación?";
      }
    ],
    found_in false,
    before [;
      Llamar:
        "Llamas a Becca, pero no obtienes respuesta.";

      Gritar:
        "Gritas el nombre de Becca, sin obtener respuesta.";

      default:
        "Becca no está.";
    ],
    life [;
      Ask:
        "Nadie responde.";
    ],
    orders [;
      NotUnderstood, Tell:
        <<Ask self>>;

      default:
        "No ves a Becca por ninguna parte.";
    ],
  has
    animate concealed;

    
! ====================================================================
! GESTORES DE TIMER
! ====================================================================


Object timer_telefono_katie
  class GestorTimer
  with
    duracion TIMER_DURACION_TLF_KATIE,
    condicion [;
      return sonido_telefono_katie.sonando();
    ],
    evento [;
      if (sonido_telefono_katie.time_left <= 0) {
        sonido_telefono_katie.time_left = sonido_telefono_katie.duracion_alarma;
        sonido_telefono_katie.time_out();
      } else {
        sonido_telefono_katie.time_left--;
      }
    ];

    
Object timer_becca
  class GestorTimer
  with
    duracion TIMER_DURACION_BECCA,
    condicion [;
      return sonido_telefono_katie.sonando();
    ],
    evento [;
      sonido_telefono_katie.each_turn();
    ];


! ====================================================================
! OBJETOS DE SOPORTE
! ====================================================================


Object barra_estado
  class objeto_barra_estado
  with
    modo BE_COMPUESTO,
    disposicion
      1  1 true BE_LOCALIDAD
      68 1 true BE_TURNOS,
    lineas_inv
      BE_INV_TOTAL;


Object sonido_telefono_katie
  class Sonido
  with
    found_in cuarto_katie pasillo_katie cocina_katie salon_katie,
    canal CANAL_TELEFONO,
    volumen_principal VOLUMEN_TELEFONO,
    fuente telefono_katie,
    duracion_alarma 10,
    time_left,
    time_out [;
      if (self.sonando()) {
        StartTimer(self, self.duracion_alarma);
        self.avisar_que_suena();
      }
    ],
    avisar_que_suena [;
      ControlTimer.PrepararImpresion();
      "^El teléfono sigue sonando.";
    ],
    sonar [ sw;
      self.tocar_sonido(Telefono1_ogg);
      StartTimer(self, self.duracion_alarma);
      if (~~sw) "Son las diez de la noche en punto y, súbitamente, comienza a
                 sonar el teléfono en la planta baja.";
    ],
    parar [ sw;
      self.parar_sonido();
      StopTimer(self);
      if (~~sw) "El teléfono deja de sonar.";
    ],
    each_turn [;
      if (self.sonando()) {
        if (becca in cocina_katie && random(100) < 10 &&
            msg_becca.at_end()) {
          ControlTimer.PrepararImpresion();
          HablaBecca("Venga ya, esto es ridículo...");
          new_line;
          telefono_katie.becca_lo_coge();
        } else if (random(100) < 20) {
          ControlTimer.PrepararImpresion();
          HablaBecca(msg_becca.run_script());
        }
      }
    ],
  has
    concealed;


Object sonido_tv_salon_katie
  class Sonido
  with
    found_in salon_katie cocina_katie pasillo_katie,
    canal CANAL_TV_SALON,
    fuente tv_salon_katie,
    volumen_principal VOLUMEN_TV_SALON,
    propagacion 2,
    sonar [;
      self.tocar_sonido(TV_Ruido_ogg);
    ],
    parar [;
      self.parar_sonido();
    ],
  has
    concealed; 


Object sonido_tv_cuarto_katie
  class Sonido
  with
    found_in salon_katie pasillo_katie,
    canal CANAL_TV_CUARTO,
    fuente tv_cuarto_katie,
    volumen_principal VOLUMEN_TV_CUARTO,
    propagacion 2,
    sonar [;
      self.tocar_sonido(TV_Ruido_ogg);
    ],
    parar [;
      self.parar_sonido();
    ],
  has
    concealed; 


Object testigo
  class Movil;


Object msg_becca
  class StopEventList
  with
    event_list "Katie... Está sonando el teléfono..."
               "¿Qué pasa? ¿No lo coges?"
               "¿Por qué no lo coges?"
               "¡Pero cógelo ya! ¿Tienes miedo o qué?";
  

! ====================================================================
! TEMAS RAIZ
! ====================================================================


! Cuidado con los infinitivos. Si pones un infinitivo aquí, luego no se puede
! usar como verbo

Object TemasBecca;

Object tema_becca_telefono "el teléfono" TemasBecca
  with
    name 'telefono' 'tlf' 'auricular';

Object tema_becca_tv "el televisor" TemasBecca
  with
    name 'televisor' 'tv' 'television' 'pantalla';

    
!===============================================================================
! (9) Otras Rutinas Reemplazadas; Rutinas Propias del Juego
!-------------------------------------------------------------------------------


[ InicioPrologo
  esp v;

  v = Damusix.QueVolumenCanal(CANAL_FONDO);
  Damusix.AsignarCanal(Grillos_ogg, CANAL_FONDO, 0, SONIDO_BUCLE_INFINITO);
  Damusix.TocarCanal(CANAL_FONDO);
  Damusix.SimpleFadeIn(Grillos_ogg, 2000, 50);

  if (hayGraficos) {
    glk_window_close(gg_bigwin, 0);
    gg_bigwin = glk_window_open(gg_mainwin,
                                winmethod_Above + winmethod_Proportional, 100,
                                wintype_Graphics, GG_BIGWIN_ROCK);
    MostrarImagenGradualmente(Prologo_png, 30, 10);
    KeyDelay(300);
    MostrarImagenGradualmente(Fundido_png, 40, 5);
  } else {
    print "Prólogo";
  }
  
  closeGraphicWindow();

  esp = 1000;
  print "Condado de Eola.";
  KeyDelay(esp / 2);
  print " Diez menos cuarto de la noche.";
  KeyDelay(esp);
  print "^^Dos jóvenes, dos amigas, solas, disfrutando de una noche de chicas
         en casa de una de ellas aprovechando que sus padres no están. Una es
         Becca Kotler, y la otra eres tú: Katie Embry. Solas, en tu casa,
         donde reina un silencio ahogado, tan sólo roto por el cantar de los
         grillos en el exterior y el sonido del programa de televisión que
         estáis viendo en tu habitación.";
!  print "^^Tú, Katie Embry, una adolescente, pasando la noche en compañía de
!         tu amiga y compañera de clase, Becca Kotler. Solas, en tu casa,
!         disfrutando de una noche de chicas aprovechando que tus padres no
!         están. Reina el silencio, tan sólo roto por el sonido del programa de
!         televisión que estáis viendo en tu habitación.";
  Damusix.SimpleFadeOut(Grillos_ogg, 2000, 10);
  Damusix.AsignarCanal(TV_Fondo_ogg, CANAL_TV_SALON, 0);
  Damusix.TocarCanal(CANAL_TV_SALON);
  Damusix.SimpleFadeIn(TV_Fondo_ogg, 2000, VOLUMEN_TV_SALON);
  KeyDelay(esp * 2);
  new_line;
  HablaBecca("La tele es un rollo. Toma: pon lo que quieras.");
  print "^Becca te da el mando del televisor.";
  KeyDelay(esp);
  print " Nada más cogerlo, apagas la tele y tiras el mando lejos de ti,
         cayendo por el lado contrario de la cama.^";
  Damusix.PararCanal(CANAL_TV_SALON);
  Damusix.TocarV(TV_Apaga_ogg);
  HablaKatie("No hay nada que me guste.", true);
  print " -comentas, aburrida. Y tras unos instantes jugando con un bucle de
         tu pelo, continúas:^";
  KeyDelay(esp);
  HablaKatie("Dicen que hay tantas ondas magnéticas a nuestro alrededor que
              perdemos como veinte veces más neuronas de lo normal... El
              gobierno lo sabe pero no hacen nada. Es una consipiración.");
  KeyDelay(esp);
  HablaBecca("Sé algo mejor: ¿Sabes lo de la cinta?");
  KeyDelay(esp);
  HablaKatie("¿Qué cinta?");
  KeyDelay(esp);
  print "^Becca se incorpora y te mira mientras empieza a contar:^"; 
  HablaBecca("Una cinta de vídeo, que parece normal... Se alquila, creo,
              no sé... Pero cuando la pones...");
  KeyDelay(esp);
  print "^Ahora eres tú la que se incorpora, con expresión rígida:^";
  HablaKatie("¿Qué? ¿Qué pasa?");
  KeyDelay(esp);
  print "^Becca, con expresión de divertida malicia, continúa su historia:^";
  print (s_user1) "^BECCA: ", "~Es como una pesadilla...";
  KeyDelay(esp);
  print " Y luego ves a una mujer... Y la mujer te sonríe...";
  KeyDelay(esp);
  print (s_emph) " Te ve a través de la pantalla...~";
  KeyDelay(esp);
  print "^^Tus ojos reflejan el miedo que va creciendo en tu interior, lo que
         divierte más a Becca, que continúa:";
  print (s_user1) "^^BECCA: ", "~Y cuando se acaba la cinta... Suena el
        teléfono... Alguien sabe que lo has visto...";
  KeyDelay(esp);
  print " Y te dice: ";
  KeyDelay(esp);
  print (s_emph) "'Morirás en siete días'.~";
  KeyDelay(esp / 2);
  new_line;
  HablaKatie("¿Quién te lo ha contado?");
  KeyDelay(esp);
  HablaBecca("No sé, alguien debe haber...");
  print "^La interrumpes:^";
  HablaKatie("¿Quién ha sido?");
  KeyDelay(esp);
  print "^Logras preocupar a Becca:^";
  HablaBecca("¿Qué te pasa?");
  KeyDelay(esp * 2);
  HablaKatie("Yo lo he visto. Josh y yo lo vimos el viernes pasado.");
  KeyDelay(esp);
  HablaBecca("Venga ya, Katie... ¡Es un cuento! Pero... ¿no estabas con tus
              padres? ¿¿Estuviste con Josh el fin de semana??");
  new_line;
  print (s_user1) "YO: ~", "Alquilamos una cabaña en las montañas, con unos
        amigos suyos...";
  KeyDelay(esp / 2);
  print " Estaban intentando grabar el partido, la recepción era muy mala...~";
  KeyDelay(esp / 2);
  new_line;
  HablaBecca("¿De qué me estás hab...?");
  new_line;
  print (s_user1) "YO: ~", "¡Escúchame! Cuando pusimos la cinta, el partido
        no estaba... Era... era...~";
  KeyDelay(esp / 2);
  new_line;
  HablaBecca("¿Qué? ¿Qué era?");
  new_line;
  print (s_user1) "YO: ", "~Era... ", (s_emph) "otra cosa...";
  KeyDelay(esp / 2);
  print " Pensamos que era una broma, pero...";
  KeyDelay(esp / 2);
  print " sonó el teléfono...";
  KeyDelay(esp / 2);
  print " Fue hace una semana.~";
  KeyDelay(esp);
  print "^^La miras a los ojos, llena de terror, y su preocupación hace crecer
         tu miedo:^^";
  print (s_user1) "YO: ", "~", (s_emph) "El plazo acaba hoy.", "~";
  KeyDelay();
  
!  if (v ~= 0) {
!    glk($00D6, 500);                      ! request_timer_events
!    glk($00D2, gg_mainwin);               ! glk_request_char_event(gg_mainwin);
!    while (1) {
!      glk($00C0, gg_arguments);           ! glk_select(gg_arguments);
!      switch (gg_arguments-->0) {
!        evtype_CharInput:
!          glk($00D3, gg_mainwin);         ! cancel_char_event
!          Damusix.SimpleFadeOut(Intro_ogg, 3000);
!          Damusix.PararCanal(CANAL_FONDO);
!          Damusix.VolumenCanal(CANAL_FONDO, v);
!          jump Exit;
! 
!        evtype_Timer:
!          glk($00D6, 500);                ! request_timer_events
! 
!        evtype_SoundNotify:
!          glk($00D6, 0);                  ! request_timer_events
!          jump Exit;
!      }
!    }
!  }
! .Exit;
!  rfalse;

  print "^^";
];


[ FinPrologo;
  print "Te acercas, paso a paso, hasta la puerta de tu habitación, mojándote
         en el proceso las plantas de los pies, lo que te provoca una
         reacción al frío más allá de lo esperable. Apoyas la mano en el
         goteante pomo de la puerta, y el frío recorre ahora los dedos y el
         brazo en dirección a tu sobresaltado corazón. Sabes que tienes que
         abrir, algo te llama... algo tan frío y básico como el agua que
         se desliza ahora por tu piel, envolviéndote. Y, sin saber bien cómo,
         te ves girando el pomo y abriendo la puerta de par en par...";
  KeyDelay(4000);
  print "^^... sólo para descubrir ", (s_emph) "el horror", ".";
  KeyDelay(2000);
  print "^^Tu boca se abre tanto como puede para gritar y gritar y no dejar
         de gritar. Tu rostro, desencajado por el puro terror, se deforma y
         se funde como en contacto con el más poderoso ácido. Y tu corazón se
         detiene justo instantes después de verlo. Sí: de verlo. Porque antes
         de morir, lo ves.";
  KeyDelay(3000);
  print "^^Ves ", (s_user1) "el círculo.";
  KeyDelay();
  BanderaFin(2);
  clearMainWindow();
  Damusix.TocarV(Mujer_Gritando_ogg);
  MostrarImagenGradualmente(Logo_Circulo_png, 40, 10);
  MostrarImagenGradualmente(Logo_Circulo_Solo_Letras_png, 40, 20);
  print "(c) 2012 Alpha Aventuras";
  WaitDelay(400);
];


[ HablaKatie msg nl;
  HablaPSI("TÚ:", msg, nl);
];


[ HablaBecca msg nl;
  HablaPSI("BECCA:", msg, nl);
];



