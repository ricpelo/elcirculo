!
! EL CÍRCULO
!
! Copyright (c) 2012 Ricardo Pérez López (Alpha Aventuras)
!

! This program is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! This program is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.


!===============================================================================
! (7) Clases del Juego
!-------------------------------------------------------------------------------


Class LugarCasaKatie
  class Lugar;


!===============================================================================
! (8) Objetos del Juego
!-------------------------------------------------------------------------------


! ====================================================================
! HABITACIONES Y DECORADOS
! ====================================================================


Object cuarto_katie "Tu habitación"
  class LugarCasaKatie
  with
    name_m 'cuarto',
    name_f 'habitacion',
    input_link "habitacion",
    nombre_direccion "tu habitación",
    adjectives 'tu',
    description [;
      "La pequeña ", (link) "lámpara", " de la ", (link) "mesita",
      " situada junto a tu ", (link) "cama", " proyecta débiles y verdosos haces
        de luz que iluminan tu habitación con un tono que, en estos momentos, te
        resulta enfermizo y asfixiante. El tamaño de tu ", (link) "cama",
      " parece pugnar por abarcar el mayor espacio posible de la estancia, sólo
        equilibrado por el pequeño ", (link) tv_cuarto_katie,
      " que observa todo sin parpadear su único ojo desde una pequeña ",
        (link) "mesa", " situada en la pared contraria.";
    ],
    out_to pasillo_katie,
    sgw_img Cuarto_Katie_jpg,
    sgw_mus 0,
    compass_look [ d;
      if (d == out_obj) {
          "A través de la puerta entreabierta observas el pasillo en penumbra.";
      }
    ];


Object decorado_cuarto_katie cuarto_katie
  class Decorado
  with
    before [;
      Enter:
        if (self.palabra == 'puerta') <<Go out_obj>>;

      Open:
        if (self.palabra == 'puerta') "Ya estaba abierta.";

      Close:
        if (self.palabra == 'puerta') {
          return becca.habla("¿Pero no vas a salir a coger el teléfono?");
        }

      Take, Push, PushDir:
        if (self.palabra == 'cama') "Es demasiado pesada.";
    ],
    sinonimos
      'mesa' 'mesita' (-1),
    describe
      'cama' "Te gusta dormir cómoda, y por ello tu cama tiene casi el doble
              de ancho que una cama normal, pero ahora, sin tener muy claro
              el por qué, se te antoja poco acogedora." G_FEMENINO
      'lampara' "Una lámpara que emite una tenue luz especialmente verdosa." 
                G_FEMENINO
      'mesita' "Una baja mesita al fondo de la habitación sostiene,
                solitaria, el televisor del dormitorio." G_FEMENINO
      'puerta' "La puerta de tu habitación permanece entreabierta." G_FEMENINO;


Object cocina_katie "Cocina"
  class LugarCasaKatie
  with
    name 'cocina',
    input_link "cocina",
    nombre_direccion "la cocina",
    description "La cocina, sobria y funcional, es el núcleo central del
                 piso inferior, el distribuidor por el que llegar a los
                 restantes lugares de la casa. El frigorífico y el teléfono
                 son los dos únicos elementos destacables en una estancia,
                 por otro lado, gélida y casi en penumbra. A través de un
                 enorme arco sin puerta se accede al salón de la casa.",
    u_to pasillo_katie,
    s_to salon_katie,
    sgw_img Cocina_Katie_jpg,
    sgw_mus 0,
    before [;
      Go:
        if (noun == u_obj) {
          if ((~~(sonido_telefono_katie.sonando())) &&
              tv_salon_katie.enchufado) {
            switch (tv_salon_katie.encendidos_solo) {
              0: if (tv_salon_katie has on) {
                   tv_salon_katie.apagar();
                   print "Justo cuando ibas a salir de la cocina para subir al
                          piso de arriba, oyes que el televisor se apaga solo
                          en el salón. Te giras para comprobar que,
                          efectivamente, el salón está a oscuras y el silencio
                          parece ahora más intenso que nunca.";
                   KeyDelay();
                   print " Instantes después, vuelve a encenderse, dejando en
                          el ambiente un sonido de estática tan carente de
                          calidez y alma como el mismo silencio.^";
                 } else {
                   print "Justo cuando ibas a salir de la cocina para subir al
                          piso de arriba, oyes que el televisor se enciende
                          solo en el salón. Te giras lentamente para comprobar
                          que, en efecto, el salón brilla y parpadea al ritmo
                          de un sonido de estática tan carente de calidez y
                          alma como el mismo silencio.^";
                 }
                 tv_salon_katie.encender();
                 katie.habla("Becca, deja de bromear... ¿Dónde estas?");
                 print "^Miras a tu alrededor, pero sigues estando sola.^";
                 rtrue;

              1: if (tv_salon_katie has on) {
                   jump NoDejarEncendido;
                 } else {
                   tv_salon_katie.se_enciende_por_segunda_vez(false);
                   rtrue;
                 }

              default:
                if (tv_salon_katie has on) {
              .NoDejarEncendido;
                  "Algo te dice que no quieres dejar el televisor encendido.
                   Algo inexplicable, irracional y primario.";
                }
            }
          }
        }
    ];


Object decorado_cocina_katie cocina_katie
  class Decorado
  with
    before [;
      Enter:
        if (self.palabra == 'escaleras') <<Go u_obj>>;
    ],
    sinonimos
      'escalera' 'escaleras' G_FEMENINO,
    describe
      'escaleras' "Las escaleras permiten el acceso a la planta alta."
                   G_FEMENINO + G_PLURAL
      'arco' "La pared sur se abre en forma de arco para acceder al salón." 0;


Object pasillo_katie "Pasillo del piso superior"
  class LugarCasaKatie
  with
    name 'pasillo',
    input_link "pasillo",
    nombre_direccion "el pasillo",
    description [;
      print "Nunca, hasta ahora, el pasillo del piso superior de tu casa
             te había parecido tan oscuro y carente de alma. Una fría
             corriente de aire lo recorre de extremo a extremo, subiendo
             por las escaleras que dan a la planta baja y cruzando tu
             espalda en forma de escalofríos continuos y persistentes.";
      if (puerta_katie notin self) {
        " La puerta abierta a tu habitación es prácticamente la única
         fuente de luz en este lúgubre pasaje."; 
      } else {
        new_line;
      }
    ],
    in_to cuarto_katie,
    d_to cocina_katie,
    sgw_img Pasillo_Katie_jpg,
    sgw_mus 0;


Object decorado_pasillo_katie pasillo_katie
  class Decorado
  with
    before [;
      Enter:
        if (self.palabra == 'puerta') <<Go in_obj>>;

      Open:
        if (self.palabra == 'puerta') "Ya estaba abierta.";

      Close:
        if (self.palabra == 'puerta')
          "Pensándolo bien, será mejor no cerrarla. Perderías la mayor parte de
           la poca luz que ilumina el pasillo.";

      Bajar:
        if (self.palabra == 'escaleras') <<Go d_obj>>;
    ],
    sinonimos
      'escalera' 'escaleras' G_FEMENINO,
    describe
      'escaleras' "Las escaleras, de madera recia y resistente, conducen
                   directamente a la cocina en la planta baja."
                   G_FEMENINO + G_PLURAL
      'puerta' "La puerta entreabierta muestra una vista parcial de tu
                habitación, cercándola como el marco de un cuadro vertical y
                alargado."
                G_FEMENINO;


Object salon_katie "Salón"
  class LugarCasaKatie
  with
    name 'salon' 'salita' 'sala',
    input_link "salon",
    nombre_direccion "el salón",
    description "El salón de tu casa está claramente dominado por un televisor
                 de gran tamaño situado sobre una mesa en la pared frontal.
                 Junto al enorme sofá a unos tres metros de la misma forman
                 en la práctica el único mobiliario disponible, exceptuando
                 un par de sillas a ambos lados del sofá y algunos cuadros
                 colgados de las paredes. Una ventana cerrada deja pasar la
                 oscuridad del exterior.",
    n_to cocina_katie,
    sgw_img Salon_Katie_jpg,
    sgw_mus 0,
    before [;
      Go:
        if (noun == n_obj && frigorifico_katie has open &&
            frigorifico_katie hasnt general) frigorifico_katie.se_abre_solo(); 
    ];


Object decorado_salon_katie salon_katie
  class Decorado
  with
    before [;
      Open, Close:
        if (self.palabra == 'ventana')
          "El mecanismo está roto desde hace mucho tiempo y no te permite
           abrir ni cerrar la ventana.";
    ],
    sinonimos
      'silla' 'sillas' G_FEMENINO,
    describe
      'ventana' "Una amplia ventana sin cortinas ni persianas abre un agujero
                 a la oscuridad que rodea la casa." G_FEMENINO
      'sillas'  "Son dos sillas, una a cada lado del sofá central."
                G_FEMENINO + G_PLURAL
      'sofa'    "Aunque sabes que resulta cómodo una vez que te sientas, al
                 tacto resulta resbaladizo, frío y duro. Permanece estático en
                 el centro de la estancia." 0
      'mesa'    "La mesa en la que se apoya el televisor tiene el tamaño justo
                 para albergar un aparato de tales dimensiones." G_FEMENINO;
    
    
! ====================================================================
! OBJETOS QUE LLEVAN LOS PERSONAJES AL EMPEZAR
! ====================================================================


Object uniforme_katie "uniforme"
  with
    name_m 'uniforme',
    name_f 'falda' 'blusa',
    description "El clásico uniforme de Instituto privado americano, compuesto
                 de una blusa blanca y una falda tableada a cuadros.",
    before [;
      Take, Wear:
        "Ya lo llevas puesto.";

      Disrobe, Drop:
        "La ocasión recomienda que sigas llevando ropa puesta...";
        
      Examine:
        rfalse;
        
      default:
        "Es mejor que dejes el uniforme como está.";
    ],
  has
    clothing worn;


! ====================================================================
! OTROS OBJETOS
! ====================================================================


Object charco_katie "charco de agua"
  with
    name 'charco' 'agua' 'liquido',
    description "Es, claramente, un charco con forma irregular
                 alimentado del agua que se filtra por debajo de la puerta
                 de tu habitación.",
    initial [;
      StartTimer(self, 4);
      "Depositado en mitad del pasillo, como una mancha líquida en el
       suelo, ves un charco de agua que se extiende filtrándose por
       debajo de la puerta cerrada de tu habitación.";
    ],
    time_left 0,
    time_out [;
      self.initial = "El charco es cada vez más grande. No deja de salir agua
                      por debajo de la puerta.";
    ],
    react_before [;
      Examine:
        if (noun == d_obj) {
          <<Examine self>>;
        }
    ],
    before [;
      Touch:
        "Rozas el charco de agua con el pulgar de uno de tus pies descalzos y
         perciben su frío y su viscosidad.";

      Smell:
        "El agua expele un hedor putrefacto, podrido, como el del agua
         estancada.";
    ],
  has
    static;


Object decorado_puerta_katie
  class Decorado
  with
    before [;
      if (action ~= ##Examine) {
        if (self.palabra_usada == 'gota' or 'gotas') {
          "Las gotas de agua son casi inmateriales y se escurren entre tus
          dedos.";
        } else if (self.palabra == 'luz') {
          "No puedes tocar la luz, pero de todas formas tampoco te atreves.";
        }
      }

      Touch:
        if (self.palabra == 'pomo') {
          "Tocas el pomo mojado de la puerta y la humedad traspasa tu piel.";
        }

      Take:
        if (self.palabra_usada == 'pomo' or 'picaporte') {
          <<Open puerta_katie>>;
        }
    ],
    sinonimos
      'picaporte'  'pomo' (-1)
      'gota'       'pomo' G_FEMENINO
      'gotas'      'pomo' G_FEMENINO + G_PLURAL
      'haz'        'luz'  0
      'resplandor' 'luz'  0
      'abertura'   'luz'  (-1),
    describe
      'pomo' "Del pomo de la puerta cae un goteo continuo de agua que no
              acabas de comprender de dónde proviene." 0
      'luz'  "La luz se proyecta desde el interior de tu habitación y sale
              por debajo de la puerta proyectando un haz de un color
              enfermizo." G_FEMENINO;


Object frigorifico_katie "frigorífico" cocina_katie
  class EscenarioAbrible
  with
    name_m 'frigorifico' 'frigo' 'congelador',
    name_f 'nevera' 'puerta',
    article "el",
    gender G_MASCULINO,
    description [;
      if (self hasnt open) {
        "Un moderno frigorífico de puertas metálicas y casi imperceptible
         ronroneo.";
      } else {
        "El frigorífico, entreabierto, emite un haz de luz azulado bañando
         parte de la cocina a su paso. El sonido del motor resulta más
         audible que cuando estaba cerrado.";
      }
    ],
    se_abre_solo [;
      give self general;
      print "Nada más cruzar el arco que separa la cocina del salón, notas
             algo moverse junto al teléfono, justo en el extremo de tu
             percepción visual. Al girar la mirada, ves la causa y el
             corazón te da un vuelco: la puerta del frigorífico se va
             abriendo poco a poco, totalmente silenciosa, hasta quedar a
             medio abrir.";
      KeyDelay();
      new_line;
    ],
    ya_visto_abierto false,
    when_open [;
      if (~~(self.ya_visto_abierto)) {
        self.ya_visto_abierto = true;
        "^Ves el frigorífico abierto, como una invitación a lo desconocido."; 
      } else {
        "^El frigorífico sigue abierto.";
      }
    ],
    before [;
      Open:
        if (self hasnt general) {
          "Abres el frigorífico, observas la comida con nulo interés, y vuelves
           a cerrarlo cuestionándote brevemente el por qué de tu falta de
           apetito.";
        } else {
          "No te atreves a volverlo a abrir.";
        }

      Close:
        if (self hasnt open) "El frigorífico ya está cerrado.";
    ],
    after [;
      Close:
        self.cerrar();
        cocina_katie.sgw_img = Cocina_Katie_jpg;
        viewImageCenter(Cocina_Katie_jpg);
        "Cierras el frigorífico con rapidez sin atreverte a mirar en su
        interior.";
    ];


Object puerta_katie "puerta de tu habitación"
  with
    name 'puerta' 'habitacion',
    article "la",
    description "Bajo la puerta cerrada de tu habitación se escurre hacia el
                 pasillo un flujo lento pero constante de agua. Un resplandor
                 blanquecino proveniente de tu habitación acompaña al río de
                 agua mientras atraviesa la rendija inferior de la puerta. Del
                 pomo, empapado como si rezumara humedad, caen continuas gotas
                 al suelo.",
    initial "Alguien ha cerrado la puerta de tu habitación, dejando bajo ella
             una fina abertura por la que salen un líquído hediondo y una
             luz mortecina.",
    add_to_scope decorado_puerta_katie,
    react_before [;
      Enter, GoIn:
        <<Open self>>;

      Go:
        if (noun == in_obj) <<Open self>>;

      Examine:
        rfalse;
        
      default:
        if (noun == decorado_puerta_katie &&
          decorado_puerta_katie.palabra_usada == 'pomo' or 'picaporte') {
          noun = puerta_katie;
          ! Esto es porque al hacer el truco de la línea anterior (de cambiar
          ! el noun por la cara) luego no se ejecuta la rutina before:
          return RunRoutines(self, before);
        }
        rfalse;
    ],
    before [;
      Examine:
        PrintOrRun(self, description);
        rtrue;

      Open:
        FinPrologo();
        rtrue;

      Llamar:
        "Llamas a la puerta, pero nadie contesta.";

      Attack:
        "La puerta es demasiado sólida y no eres lo bastante fuerte.";

      Close:
        "La puerta ya está cerrada.";
        
    ],
  has
    static female openable;


Object telefono_katie "teléfono" cocina_katie
  with
    name 'telefono' 'auricular' 'tlf',
    description "El teléfono de tu casa, de un color blanco gastado, cuelga de
                 la pared junto al frigorífico.",
!    initial "Ves tu teléfono.",
    sonando [;
      return sonido_telefono_katie.sonando();
    ],
    descolgar [;
      sonido_telefono_katie.parar(true);
      remove becca;
      becca_ausente.found_in = true;
      move becca_ausente to LugarReal();
      move charco_katie to pasillo_katie;
      move puerta_katie to pasillo_katie;
      pasillo_katie.sgw_img = Pasillo_Katie_Charco_jpg;
    ],
    becca_lo_coge [;
      self.descolgar();
      print "Becca coge el teléfono con expresión seria.^";
      becca.habla("¿Diga?", true);
      print " ---la oyes decir, casi susurrando,
            al auricular del teléfono.^^";
      print "Lentamente, a medida que escucha lo que el teléfono le dice, se
             gira hacia ti con una inclasificable mirada en los ojos, y tu
             preocupación crece por momentos...";
      KeyDelay(10000);
      print " hasta que te muestra una amplia sonrisa y pasándote el teléfono,
             divertida, te dice:^";
      becca.habla("Es tu madre.");
      new_line;
      self.coges_el_telefono();
      rtrue;
    ],
    coges_el_telefono [;
      print "Coges el teléfono, y aún con desconfianza lo acercas a tu oído.
             Tras emitir un nervioso ~¿Hola?~, oyes al otro lado de la línea
             la tranquilizadora (y enfática) voz de tu madre preguntándote qué
             tal va la noche y exhortando que no te vayas tarde a la cama.
             Durante la corta pero extrañamente tranquilizadora conversación,
             percibes la voz de Becca preguntando algo mientras se aleja de la
             cocina escaleras arriba. Finalmente, cuelgas el teléfono...";
      KeyDelay(25000);
      print (s_emph) " y estas sola.^";
      katie.habla("¿Becca?", true);
      print " ---la llamas temiendo alzar demasiado la voz, sin respuesta.^";
      katie.habla("¿BECCA?", true);
      print " ---gritas esta vez, sacando fuerzas del miedo.^^";
      print_ret (s_emph) "No hay respuesta.";
!      print (s_user1) "¿Becca?");
    ],
    before [;
      Take, Descolgar:
        if (self.sonando()) {
          self.descolgar();
          self.coges_el_telefono();
          rtrue;
        } else {
          "Descuelgas y no oyes nada. Ni siquiera el tono de marcado.";
        }

      Drop, Colgar:
        "El teléfono ya está colgado.";

      LlamarPor:
        if (self.sonando()) {
          "El teléfono está sonando. No puedes llamar tú ahora.";
        } else {
          <<Descolgar self>>;
        }
    ],
  has
    scenery;


Object tv_cuarto_katie "televisor" cuarto_katie
  with
    name_f 'tv' 'television' 'tele' 'pantalla',
    name_m 'televisor',
    nombre_link "televisor",  ! No hace falta, porque vale igual que input_link
    input_link "televisor",
    ZIPI_item
      Menu_Examinar
      Menu_Coger
      Menu_Dejar
      Menu_Encender
      Menu_Apagar,
    description [;
      print "Un pequeño televisor sin aspectos destacables. Está ";
      if (self has on) print "encendido, emitiendo nieve y ruido de estática";
      else             print "apagado";
      ".";
    ],
    gender G_MASCULINO,
!    initial "Ves el televisor.",
    encender [;
      give self general;
      sonido_tv_cuarto_katie.sonar();
      cuarto_katie.sgw_img = Cuarto_Katie_TV_Encendida_jpg;
      viewImageCenter(Cuarto_Katie_TV_Encendida_jpg);
    ],
    apagar [;
      sonido_tv_cuarto_katie.parar();
      cuarto_katie.sgw_img = Cuarto_Katie_jpg;
      viewImageCenter(Cuarto_Katie_jpg);
    ],
    before [;
      Examine:
        PrintOrRun(self, description);
        rtrue;

      Enchufar:
        "El televisor ya estaba enchufado.";
        
      Desenchufar:
        "¿Para qué quieres desenchufarl", (o) self, "?";
    ],
    after [ c;
      SwitchOn:
        c = self hasnt general;
        self.encender();
        if (c) {
          "Al encender el televisor, te sorprende comprobar que no aparece
           ninguna imagen, sino simple ruido y estática, como si no recibiera
           señal.";
        } else {
          "Vuelves a encender el televisor y el ruido granulado aparece de
           nuevo en la pantalla acompañado de estática.";
        }

      SwitchOff:
        self.apagar();
        "Apagas el televisor, sin evitar sentir cierto alivio al hacerlo.";
    ],
  has
    scenery switchable;


Object tv_salon_katie "televisor" salon_katie
  with
    name_f 'tv' 'television' 'tele' 'pantalla',
    name_m 'televisor',
    gender G_MASCULINO,
    description [;
      if (self hasnt general) {
        "Un televisor negro, impersonal, de gran tamaño, con una pantalla
         tan oscura que su superficie apenas refleja el entorno en el que
         se encuentra.";
      } else {
        if (self has on) {
          "El enorme televisor ilumina la estancia con un brillo vivo y
           cambiante, resultado del perpetuo movimiento del ruido en la
           pantalla, que te recuerda al de millones de organismos
           microscópicos moviéndose y chocando entre sí.";
        } else {
          "El televisor, ahora apagado, se presenta tan oscuro como un mal
           presagio.";
        }
      }
    ],
    describe [;
      if (self has on) {
        "^El televisor está encendido, mostrando un ruido granulado que baña
         el salón con una luz sucia y nerviosa.";
      }
      rtrue;
    ],
!    initial "Ves el televisor.",
    time_left,
    time_out [;
      if (self hasnt on) {
        self.se_enciende_por_segunda_vez(true);
!      "^Todos los músculos del cuerpo se te agarrotan y tus pulmones se quedan
!       sin aire cuando notas que el televisor del salón vuelve a encenderse
!       solo."
!      Se vuelve a encender el televisor.";
      }
    ],
    se_enciende_por_segunda_vez [ sw;
      ! sw == false si viene de cocina_katie al intentar subir
      ! sw == true  si viene del time_out de tv_salon_katie
      self.encender();
      cuarto_katie.sgw_mus  = Gathering_Darkness_ogg;
      pasillo_katie.sgw_mus = cuarto_katie.sgw_mus;  
      cocina_katie.sgw_mus  = cuarto_katie.sgw_mus;
      salon_katie.sgw_mus   = cuarto_katie.sgw_mus;
      Damusix.AsignarCanal(real_location.sgw_mus, CANAL_FONDO,
                           CalcVol(real_location.sgw_vol), -1);
      Damusix.TocarCanal(CANAL_FONDO);
      if (~~sw) {
        print "El televisor se vuelve a encender. Solo. Otra vez.
               Más sonido de estática proveniente del salón,
               transformando la atmósfera en pura inquietud. Ahora
               sí que puedes sentir con claridad los latidos de tu
               corazón rebotando en tu pecho de pura ansiedad.^";
      } else {
        if (LugarReal() ~= salon_katie) {
          print "^Un momento... ¿Eso que oyes... no es el televisor?^";
        }
        print "^Durante unos instantes no acabas de creerlo... Buscas una
               explicación lógica, intentas convencerte a ti misma de que no
               es cierto... Pero la realidad, injusta pero imparcial, te lo
               deja claro: el televisor del salón ha vuelto a encenderse
               solo.^";
      }
      katie.habla("¡Becca, vale ya!");
      "^Pero Becca no está. Y te quedas ahí, sin subir la escalera, mirando
       en dirección al salón y el televisor encendido.";
    ],
    enchufado true,
    encendidos_solo 0,
    encender [ sw;
      give self general;
      give self on;
      sonido_tv_salon_katie.sonar();
      salon_katie.sgw_img = Salon_Katie_TV_Encendida_jpg;
      if (location == salon_katie) {
        viewImageCenter(Salon_Katie_TV_Encendida_jpg);
      }
      if (~~sw) self.encendidos_solo++;
    ],
    apagar [;
      give self ~on;
      sonido_tv_salon_katie.parar();
      salon_katie.sgw_img = Salon_Katie_jpg;
      if (location == salon_katie) {
        viewImageCenter(salon_katie.sgw_img);
      }
    ],
    before [;
      Examine:
        PrintOrRun(self, description);
        rtrue;

      SwitchOn:
!        if (~~(self.enchufado))
!          "No se puede encender porque está desenchufado.";
        if (self has on) "Ya está encendido.";
        rfalse;
        
      SwitchOff:
!        if (~~(self.enchufado))
!          "Ya está desenchufado.";
        if (self hasnt on) "Ya está apagado.";
        rfalse;

      Enchufar:
        "El televisor ya está enchufado.";
        
      Desenchufar:
        "El cable del enchufe pasa por la parte trasera y no alcanzas a tirar
         de él.";
        
!      Tie:
!        if (self.enchufado) {
!          "El televisor ya está enchufado.";
!        } else {
!          self.enchufado = true;
!          "Enchufas el televisor.";
!        }

!      Desenchufar:
!        if (~~(self.enchufado)) {
!          "El televisor ya está desenchufado.";
!        } else {
!          self.enchufado = false;
!          if (self has on) {
!            self.apagar();
!            "Desenchufas el televisor. Al hacerlo, la pantalla se apaga.";
!          } else {
!            "Desenchufas el televisor.";
!          }
!        }     
    ],
    after [;
      SwitchOn:
        self.encender(true);
        StopTimer(self);
        if (self hasnt general) {
          give self general;
          "Al encender el televisor compruebas que no aparece ninguna emisión
           salvo la típica imagen de ruido granulado y el sonido de estática
           que se oye siempre que no se capta ninguna señal.";
        } else {
          "Vuelves a encender el televisor y éste vuelve a mostrar ruido y a
           emitir sonido de estática.";
        }

      SwitchOff:
        self.apagar();
        switch (self.encendidos_solo) {
          1: StartTimer(self, 3);
          2: frigorifico_katie.abrir();
             cocina_katie.sgw_img = Cocina_Frigo_Abierto_jpg;
        }
        "Con un gesto rápido, apagas el televisor y la estática desaparece,
         retornando la estancia a la semioscuridad.";
    ],
  has
    static switchable;


! ====================================================================
! PERSONAJES
! ====================================================================


Object becca "Becca" cuarto_katie
  class Personaje
  with
    name 'becca',
    short_name "Becca",
    description "Becca es, sin duda, tu mejor amiga, y lo es desde os
                 conocísteis, hace ya siglos. Es morena, con grandes ojos
                 verdes, y de carácter un tanto rebelde. Compañeras de
                 Instituto que sois, lleva puesto el mismo uniforme que tú.",
    initial "^Junto a ti está Becca.",
    input_link "Becca",
    imagen Becca_jpg,
    describe [;
      switch (random(3)) {
        1: "^A tu lado, tu amiga Becca te observa con inquietud.";
        2: "^", (link) self, " te mira esperando qué vas a hacer.";
        3: "^Tu amiga Becca está a tu lado.";
      }
    ],
    react_before [;
      Gritar:
        self.habla("¡¿Pero qué te pasa?! ¿Qué ocurre? ¿A qué vienen esos
                    gritos?");
        rtrue;
    ],
    react_after [ c;
      SwitchOn:
        if (noun == tv_cuarto_katie or tv_salon_katie) {
          c = tv_cuarto_katie hasnt general && tv_salon_katie hasnt general;
          ! Lo hago así para que el mensaje de Becca aparezca DESPUÉS de los
          ! mensajes que genere la rutina after:
          RunRoutines(noun, after);
          if (c) self.habla("Qué extraño... ¿Qué ha pasado con la imagen?");
          else   self.habla("Sigue sin haber imagen...");
          rtrue;
        }
    ],
    before [;
      Llamar:
        return self.habla("Estoy aquí... ¿No me ves?");
    ],
    life [
      tema;
      Ask:
        tema = AveriguarTema(TemasBecca);
        switch (tema) {
          tema_becca_telefono:
            self.habla("¿Crees que puede ser la misma persona que te llamó
                        en la cabaña?");
            PreguntaSiNo = 1;

          tema_becca_tv:
            if (tv_cuarto_katie has general || tv_salon_katie has general) {
              self.habla("Parece que se ha ido la señal.");
            } else {
              self.habla("Sí, haces bien en apagar la tele. Es un rollo.");
            }
            
          default:
            self.habla("No entiendo qué quieres decirme.");
        }
        rtrue;

      default:
        return self.habla("No te entiendo...");
    ],
    orders [;
      if (player == self) rfalse;

      Venir, Seguir:
        return self.habla("Te sigo, Katie, ya te sigo.");

      Go, GoIn, Enter, Exit, Subir, Bajar:
        return self.habla("No pienso ir sola a ninguna parte.");
        
      Take:
        if (noun == telefono_katie) {
          if (LugarReal() == cocina_katie) {
            return telefono_katie.becca_lo_coge();
          } else {
            if (cocina_katie has visited) {
              return self.habla("El teléfono está en la cocina.");
            } else {
              return self.habla("¿Dónde está el teléfono?");
            }
          }
        }

      Saludar:
        return self.habla("Katie, déjate de saludos ahora. El teléfono sigue
                           sonando.");

      default:
        return self.habla("Mejor hazlo tú misma.");
    ],
  has
    female;


Object becca_ausente "Becca ausente"
  with
    name 'becca',
    description [;
      if (location == cocina_katie or salon_katie) {
        "No ves a Becca por ninguna parte. ¿Estará arriba?";
      } else {
        "No ves a Becca por ninguna parte. ¿Estará en tu habitación?";
      }
    ],
    found_in false,
    before [;
      Llamar:
        "Llamas a Becca, pero no obtienes respuesta.";

      Gritar:
        "Gritas el nombre de Becca, sin obtener respuesta.";

      default:
        "Becca no está.";
    ],
    life [;
      Ask:
        "Nadie responde.";
    ],
    orders [;
      NotUnderstood, Tell:
        <<Ask self>>;

      default:
        "No ves a Becca por ninguna parte.";
    ],
  has
    animate concealed;


Object katie "Katie" cuarto_katie
  class Personaje
  with
    name 'katie',
    short_name "Katie",
    add_to_scope uniforme_katie,
    imagen Katie_jpg,
    description "Eres tú: Katie Embry, una chica adolescente algo alocada pero
                 responsable, de ojos oscuros y cabello largo, rubio y liso.
                 Tu mejor amiga Becca y tú estáis pasando una noche solas en tu
                 casa, aprovechando que tus padres están fuera. Aún llevas
                 puesto tu uniforme escolar, aunque vas descalza.",
  has
    female;

    
! ====================================================================
! GESTORES DE TIMER
! ====================================================================

    
Object timer_becca
  class GestorTimer
  with
    duracion TIMER_DURACION_BECCA,
    condicion [;
      return sonido_telefono_katie.sonando();
    ],
    evento [;
      sonido_telefono_katie.each_turn();
    ];


Object timer_telefono_katie
  class GestorTimer
  with
    duracion TIMER_DURACION_TLF_KATIE,
    condicion [;
      return sonido_telefono_katie.sonando();
    ],
    evento [;
      if (sonido_telefono_katie.time_left <= 0) {
        sonido_telefono_katie.time_left = sonido_telefono_katie.duracion_alarma;
        sonido_telefono_katie.time_out();
      } else {
        sonido_telefono_katie.time_left--;
      }
    ];


! ====================================================================
! OBJETOS DE SOPORTE
! ====================================================================


Object sonido_telefono_katie
  class Sonido
  with
    found_in cuarto_katie pasillo_katie cocina_katie salon_katie,
    emision Telefono1_ogg,
    canal CANAL_TELEFONO,
    volumen_principal VOLUMEN_TELEFONO,
    fuente telefono_katie,
    duracion_alarma 10,
    time_left,
    time_out [;
      if (self.sonando()) {
        StartTimer(self, self.duracion_alarma);
        self.avisar_que_suena();
      }
    ],
    avisar_que_suena [;
      ControlTimer.PrepararImpresion();
      switch (LugarReal()) {
        cuarto_katie: "^El teléfono sigue sonando fuera, en la planta baja.";
        pasillo_katie: "^El teléfono sigue sonando en la planta baja.";
        cocina_katie: "^El teléfono sigue sonando.";
        salon_katie: "^El teléfono sigue sonando en la cocina.";
      }
    ],
    sonar [ sw;
      self.tocar_sonido();
      StartTimer(self, self.duracion_alarma);
      if (~~sw) "Son las diez de la noche en punto y, súbitamente, comienza a
                 sonar el teléfono en la planta baja.";
    ],
    parar [ sw;
      self.parar_sonido();
      StopTimer(self);
      if (~~sw) "El teléfono deja de sonar.";
    ],
    ultimo_mensaje 0,
    each_turn [;
      if (self.sonando()) {
        if (becca in cocina_katie && random(100) < 10 &&
            self.ultimo_mensaje == 4) {
          ControlTimer.PrepararImpresion();
          becca.habla("Venga ya, esto es ridículo...");
          new_line;
          telefono_katie.becca_lo_coge();
        } else if (random(100) < 20) {
          ControlTimer.PrepararImpresion();
          self.ultimo_mensaje = becca.hablalast(
            "Katie... Está sonando el teléfono...",
            "¿Qué pasa? ¿No vas a cogerlo?",
            "¿Por qué no lo coges?",
            "¡Pero cógelo ya! ¿Tienes miedo o qué?"
          );
        }
      }
    ];


Object sonido_tv_cuarto_katie
  class Sonido
  with
    found_in salon_katie pasillo_katie,
    emision TV_Ruido_ogg, 
    canal CANAL_TV_CUARTO,
    fuente tv_cuarto_katie,
    volumen_principal VOLUMEN_TV_CUARTO,
    propagacion 2;


Object sonido_tv_salon_katie
  class Sonido
  with
    found_in salon_katie cocina_katie pasillo_katie,
    emision TV_Ruido_ogg,
    canal CANAL_TV_SALON,
    fuente tv_salon_katie,
    volumen_principal VOLUMEN_TV_SALON,
    propagacion 2;


!Object msg_becca
!  class StopEventList
!  with
!    event_list "Katie... Está sonando el teléfono..."
!               "¿Qué pasa? ¿No lo coges?"
!               "¿Por qué no lo coges?"
!               "¡Pero cógelo ya! ¿Tienes miedo o qué?";
  

! ====================================================================
! TEMAS RAIZ
! ====================================================================


! Cuidado con los infinitivos. Si pones un infinitivo aquí, luego no se puede
! usar como verbo

Object TemasBecca;

Object tema_becca_telefono "el teléfono" TemasBecca
  with
    name 'telefono' 'tlf' 'auricular';

Object tema_becca_tv "el televisor" TemasBecca
  with
    name 'televisor' 'tv' 'television' 'pantalla';

    
!===============================================================================
! (9) Otras Rutinas Reemplazadas; Rutinas Propias del Juego
!-------------------------------------------------------------------------------


[ FinPrologo
  i j sw;
  StatusLineHeight(0);
  print "Te acercas, paso a paso, hasta la puerta de tu habitación, mojándote
         en el proceso las plantas de los pies, lo que te provoca una
         reacción al frío más allá de lo esperable. Apoyas la mano en el
         goteante pomo de la puerta, y el frío recorre ahora los dedos y el
         brazo en dirección a tu sobresaltado corazón. Sabes que tienes que
         abrir, algo te llama... algo tan frío y básico como el agua que
         se desliza ahora por tu piel, envolviéndote. Y, sin saber bien cómo,
         te ves girando el pomo y abriendo la puerta de par en par...";
  KeyDelay();
  print "^^... sólo para descubrir ", (s_emph) "el horror", ".";
  viewImageCenter(Fin_Prologo1_jpg);
  KeyDelay();
  viewImageCenter(Fin_Prologo2_jpg);
  print "^^Tu boca se abre tanto como puede para gritar y gritar y no dejar
         de gritar. Tu rostro, desencajado por el puro terror, se deforma y
         se funde como en contacto con el más poderoso ácido. Y tu corazón se
         detiene justo instantes después de verlo.";
  viewImageCenter(Fin_Prologo3_jpg);
  KeyDelay();
  print "^^Sí: de verlo. Porque antes de morir, ", (s_emph) "lo ves.";
  KeyDelay(10000);
  print "^^Ves ", (s_user1) "el círculo.";
  KeyDelay(2000);
  viewImageCenter(Fin_Prologo4_jpg);
  Damusix.TocarV(Mujer_Gritando_ogg);
  WaitDelay(500);
  viewImageCenter(Fin_Prologo5_jpg);
  Damusix.SimpleFadeOut(Gathering_Darkness_ogg, 1000);
  clearMainWindow();
  print "Ves ", (s_user1) "el círculo.";
  WaitDelay(100);
  MostrarImagenGradualmente(Logo_Circulo_png, 40, 50);
  MostrarImagenGradualmente(Logo_Circulo_Solo_Letras_png, 40, 100);
  clearTextWindow();
  box "(c) 2012 Alpha Aventuras";
  KeyDelay();
!  BanderaFin(2);
  Damusix.AsignarCanal(TV_Ruido_ogg, CANAL_TV_SALON, VOLUMEN_TV_SALON,
                       SONIDO_REPETIR);
  Damusix.TocarCanal(CANAL_TV_SALON);
  for (j = 1: j <= 3: j++) {
    sw = false;
    for (i = 0: i < 40: i++) {
      viewImageCenter(Nieve_TV1_jpg); if (KeyDelay(25)) break;
      viewImageCenter(Nieve_TV2_jpg); if (KeyDelay(25)) break;
      viewImageCenter(Nieve_TV3_jpg); if (KeyDelay(25)) break;
      if (~~sw) {
        sw = true;
        switch (j) {
          1: box "Programación y diseño: Ricardo Pérez (Sothoth)";
          2: box "Gráficos: Manuel Millán (Ubik)";
          3: box " Basada en la novela @<<Ringu@>>, de Koji Suzuki"
                 "y el film @<<The Ring@>>, (c) 2002 Dreamworks SKG";
        }
      }
    }
  }
  Damusix.TocarV(TV_Apaga_ogg);
  Damusix.PararCanal(CANAL_TV_SALON);
  barra_estado.numero_lineas = 1;
  barra_estado.dibujar();
  deadflag = 2;
!  InicioHistoria();
];


[ InicioPrologo
  esp;

  Damusix.PararCanal(CANAL_FONDO);
  Damusix.AsignarCanal(Grillos_ogg, CANAL_TELEFONO, 0, SONIDO_REPETIR);
  Damusix.TocarCanal(CANAL_TELEFONO);
  Damusix.SimpleFadeIn(Grillos_ogg, 2000, 50);

  if (hayGraficos) {
    glk_window_close(gg_bigwin, 0);
    gg_bigwin = glk_window_open(gg_mainwin,
                                winmethod_Above + winmethod_Proportional, 100,
                                wintype_Graphics, GG_BIGWIN_ROCK);
    MostrarImagenGradualmente(Prologo_png, 30, 50);
    KeyDelay(1500);
    MostrarImagenGradualmente(Fundido_png, 40, 25);
  } else {
    print "Prólogo";
  }
  
  closeGraphicWindow();
  ChangePlayer(katie);

  esp = 5000;
  print "Condado de Eola.";
  KeyDelay(esp / 2);
  print " Febrero de 2002.";
  KeyDelay(esp / 2);
  print " Diez menos cuarto de la noche.";
  KeyDelay(esp);
  print "^^Dos jóvenes, dos amigas, solas, disfrutando de una noche de chicas
         en casa de una de ellas aprovechando que sus padres no están. Una es
         Becca Kotler, y la otra eres tú: Katie Embry. Solas, en tu casa,
         donde reina un silencio ahogado, tan sólo roto por el cantar de los
         grillos en el exterior y el sonido del programa de televisión que
         estáis viendo en tu habitación.";
!  print "^^Tú, Katie Embry, una adolescente, pasando la noche en compañía de
!         tu amiga y compañera de clase, Becca Kotler. Solas, en tu casa,
!         disfrutando de una noche de chicas aprovechando que tus padres no
!         están. Reina el silencio, tan sólo roto por el sonido del programa de
!         televisión que estáis viendo en tu habitación.";
  Damusix.SimpleFadeOut(Grillos_ogg, 2000, 10);
  Damusix.AsignarCanal(TV_Fondo_ogg, CANAL_TV_SALON, 0);
  Damusix.TocarCanal(CANAL_TV_SALON);
  Damusix.SimpleFadeIn(TV_Fondo_ogg, 2000, VOLUMEN_TV_SALON);
  KeyDelay();
  new_line;
  becca.habla("La tele es un rollo. Toma: pon lo que quieras.");
  print "^Becca te da el mando del televisor.";
  KeyDelay();
  print " Nada más cogerlo, apagas la tele y tiras el mando lejos de ti,
         cayendo por el lado contrario de la cama.^";
  Damusix.PararCanal(CANAL_TV_SALON);
  Damusix.TocarV(TV_Apaga_ogg);
  katie.habla("No hay nada que me guste.", true);
  print " ---comentas, aburrida. Y tras unos instantes jugando con un bucle de
         tu pelo, continúas:^";
  KeyDelay();
  katie.habla("Dicen que hay tantas ondas magnéticas a nuestro alrededor que
              perdemos como veinte veces más neuronas de lo normal... El
              gobierno lo sabe pero no hacen nada. Es una consipiración.");
  KeyDelay();
  becca.habla("Sé algo mejor: ¿Sabes lo de la cinta?");
  KeyDelay();
  katie.habla("¿Qué cinta?");
  KeyDelay();
  print "^Becca se incorpora y te mira mientras empieza a contar:^"; 
  becca.habla("Una cinta de vídeo, que parece normal... Se alquila, creo,
              no sé... Pero cuando la pones...");
  KeyDelay();
  print "^Ahora eres tú la que se incorpora, con expresión rígida:^";
  katie.habla("¿Qué? ¿Qué pasa cuando la pones?");
  KeyDelay();
  print "^Becca, con expresión de divertida malicia, continúa su historia:^";
  becca.habla_inicio();
  print "Es como una pesadilla...";
  KeyDelay();
  print " Y luego ves a una mujer... Y la mujer te sonríe...";
  KeyDelay();
  print (s_emph) " Te ve a través de la pantalla...";
  becca.habla_fin(true);
  KeyDelay();
  print "^^Tus ojos reflejan el miedo que va creciendo en tu interior, lo que
         divierte más a Becca, que continúa:^";
  becca.habla_inicio();
  print "Y cuando se acaba la cinta... Suena el
        teléfono... Alguien sabe que lo has visto...";
  KeyDelay();
  print " Y te dice: ";
  KeyDelay();
  print (s_emph) "'Morirás en siete días'.";
  becca.habla_fin();
  KeyDelay(esp / 2);
  katie.habla("¿Quién te lo ha contado?");
  KeyDelay();
  becca.habla("No sé, alguien debe haber...");
  print "^La interrumpes:^";
  katie.habla("¿Quién ha sido?");
  KeyDelay();
  print "^Logras preocupar a Becca:^";
  becca.habla("¿Qué te pasa?");
  KeyDelay();
  katie.habla("Yo lo he visto. Josh y yo lo vimos el viernes pasado.");
  KeyDelay();
  becca.habla("Venga ya, Katie... ¡Es un cuento! Pero... ¿no estabas con tus
              padres? ¡¿Estuviste con Josh el fin de semana?!");
  katie.habla_inicio();
  print "Alquilamos una cabaña en las montañas, con unos
        amigos suyos...";
  KeyDelay();
  print " Estaban intentando grabar el partido, la recepción era muy mala...";
  katie.habla_fin();
  KeyDelay();
  becca.habla("¿De qué me estás hab...?");
  katie.habla("¡Escúchame! Cuando pusimos la cinta, el partido
        no estaba... Era... era...");
  KeyDelay();
  becca.habla("¿Qué? ¿Qué era?");
  katie.habla_inicio();
  print "Era... ", (s_emph) "otra cosa...";
  KeyDelay();
  print " Pensamos que era una broma, pero...";
  KeyDelay();
  print " sonó el teléfono...";
  KeyDelay();
  print " Fue hace una semana.";
  katie.habla_fin();
  KeyDelay();
  print "^La miras a los ojos, llena de terror, y su preocupación hace crecer
         tu miedo:^";
  katie.habla_inicio();
  print (s_emph) "El plazo acaba hoy.";
  katie.habla_fin();
  KeyDelay();
  
!  if (v ~= 0) {
!    glk($00D6, 500);                      ! request_timer_events
!    glk($00D2, gg_mainwin);               ! glk_request_char_event(gg_mainwin);
!    while (1) {
!      glk($00C0, gg_arguments);           ! glk_select(gg_arguments);
!      switch (gg_arguments-->0) {
!        evtype_CharInput:
!          glk($00D3, gg_mainwin);         ! cancel_char_event
!          Damusix.SimpleFadeOut(Intro_ogg, 3000);
!          Damusix.PararCanal(CANAL_FONDO);
!          Damusix.VolumenCanal(CANAL_FONDO, v);
!          jump Exit;
! 
!        evtype_Timer:
!          glk($00D6, 500);                ! request_timer_events
! 
!        evtype_SoundNotify:
!          glk($00D6, 0);                  ! request_timer_events
!          jump Exit;
!      }
!    }
!  }
! .Exit;
!  rfalse;

  new_line;
];


#ifdef DEBUG;
[ TestPrologo;
  test_machine.clear();
  test_machine.inserta("sal");
  test_machine.inserta("baja");
  test_machine.inserta("coge telefono");
  test_machine.inserta("sube");
  test_machine.inserta("s");
  test_machine.inserta("apaga tv");
  test_machine.inserta("n");
  test_machine.inserta("sube");
  test_machine.inserta("s");
  test_machine.inserta("apaga tv");
  test_machine.inserta("n");
  test_machine.inserta("cierra frigorifico");
  test_machine.inserta("sube");
  test_machine.inserta("abre puerta");
];
#endif;


!===============================================================================
! (10) Verbos y Gramaticas Propias del Juego
!-------------------------------------------------------------------------------


#ifdef DEBUG;
Verb meta 'testprologo'
  *                      -> TestPrologo
  * 'full'               -> TestPrologoFull;
#endif;


!===============================================================================
! (11) Rutinas de Acciones Propias del Juego
!-------------------------------------------------------------------------------


#ifdef DEBUG;
[ TestPrologoFullSub;
  TestPrologo();
  test_machine.run();
];


[ TestPrologoSub;
  TestPrologo();
  test_machine.run_once();
];
#endif;


! Fin prologo.inf
